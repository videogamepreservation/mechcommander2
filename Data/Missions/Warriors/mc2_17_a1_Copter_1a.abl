//****************************************************************************************//
// 
//		Brains used for Renard in his most Awesome Atlas
//
//      MISSION:  Mc2_17
//
//
//****************************************************************************************//

fsm mc2_17_a1_Copter_1a;

	var

		static WorldPosition		startPosition;
		static PatrolState		startPatrolState, BasePatrolState;
		static PatrolPath			startPatrolPath, BasePatrolPath;
		static boolean			willRequestHelp;
		static real				lastHelpRequestTime;
		static real				helpRequestFrequency;
		static real				attackerHelpRadius;
		static real				defenderHelpRadius;
		static integer			AttackStateHandle;

		static boolean			poweredDown;


		static integer			numFunctionalWeapons;
		static integer[20]		weaponList;
		static boolean			attackBase;
		static real				TurnOffSensor;

//****************************************************************************************

function init;

    code

		//setDebugWindow(-1, -1);

		//---------------------------
		// Grab his start position...
		getObjectPosition(-1, startPosition);

		//-------------------
		// Look for movers...
		setTargetPriority(0, TARGET_PRIORITY_CURTARGET, -1, 0, CONTACT_CRITERIA_ENEMY + CONTACT_CRITERIA_VISUAL_OR_SENSOR + CONTACT_CRITERIA_NOT_DISABLED);
		setTargetPriority(1, TARGET_PRIORITY_MOVER, 0, 0, CONTACT_CRITERIA_ENEMY + CONTACT_CRITERIA_VISUAL_OR_SENSOR + CONTACT_CRITERIA_NOT_DISABLED);
		setTargetPriority(2, TARGET_PRIORITY_NONE, 0, 0, 0);

		AttackStateHandle = getStateHandle("attack");
		attackBase = False;

		startPatrolState[0] = PATROL_TYPE_LINEAR;    
		startPatrolState[1] = 7;    //?num points
		startPatrolState[2] = 1;    //?num cycles
		startPatrolState[3] = PATROL_DIRECTION_FORWARD;    
		startPatrolState[4] = -1;    //reset cur point
		startPatrolState[5] = -1;    //reset cur cycle
		startPatrolState[6] = CONTACT_CRITERIA_ENEMY + CONTACT_CRITERIA_VISUAL_OR_SENSOR + CONTACT_CRITERIA_NOT_DISABLED;    
		startPatrolPath[0, 0] = -6762 - 44;
		startPatrolPath[0, 1] = -362;
		startPatrolPath[1, 0] = -6421 - 44;
		startPatrolPath[1, 1] = -661;
		startPatrolPath[2, 0] = -5866 - 44;
		startPatrolPath[2, 1] = -1216;
		startPatrolPath[3, 0] = -5104 - 44;
		startPatrolPath[3, 1] = -1514;
		startPatrolPath[4, 0] = -3605 - 44;
		startPatrolPath[4, 1] = -4501;
		startPatrolPath[5, 0] = -1002 - 44;
		startPatrolPath[5, 1] = -4160;
		startPatrolPath[6, 0] = 1173 - 44;
		startPatrolPath[6, 1] = -2709;	

		BasePatrolState[0] = PATROL_TYPE_LINEAR;    
		BasePatrolState[1] = 2;    //?num points
		BasePatrolState[2] = -1;    //?num cycles
		BasePatrolState[3] = PATROL_DIRECTION_FORWARD;    
		BasePatrolState[4] = -1;    //reset cur point
		BasePatrolState[5] = -1;    //reset cur cycle
		BasePatrolState[6] = CONTACT_CRITERIA_ENEMY + CONTACT_CRITERIA_VISUAL_OR_SENSOR + CONTACT_CRITERIA_NOT_DISABLED;    
		BasePatrolPath[0, 0] = 234 - 44;
		BasePatrolPath[0, 1] = -192;
		BasePatrolPath[1, 0] = -874 - 44;
		BasePatrolPath[1, 1] = -3221;


		willRequestHelp = true; //?true or false
		helpRequestFrequency = 20.0; //?in secs
		attackerHelpRadius = 100; //?in meters
		defenderHelpRadius = 125; //?in meters
		lastHelpRequestTime = -100.0;

		setWillHelp(False);
						
endfunction;


//----------------------------------------------------------------------------------------

function setWillRequestHelp (boolean setting);

	code

		if (setting and (not willRequestHelp)) then
			lastHelpRequestTime = 0.0;
		endif;
		willRequestHelp = setting;

endfunction;

//----------------------------------------------------------------------------------------

function update : integer;

	var

		boolean		processingPilotEvents;
		boolean		thinking;
		integer		pilotEventID;
		integer		pilotState;
		integer[20]		pilotEventParams;
		integer		curTarget;
		real			curTime;
		real[3]		myPos;
		real[3]		attackerPos;
		real			distanceToAttacker;
		integer		curStateHandle;


	code

		curTime = getTime;
		curStateHandle = getCurrentStateHandle;


		//--------------------------------------------------
		// Process the pilot events since the last update...
		numFunctionalWeapons = getWeapons(weaponList, 1);
		if (numFunctionalWeapons == 0) then
			trans noWeapons;
		endif;

		//--------------------------------------------------
		// Check to see if time to attack the player
		if (LiaoSonAttack) and (Not attackBase) then
			attackBase = True;
			TurnOffSensor = getTime + 20;
			trans attackingBase;
		endif;
			

		processingPilotEvents = TRUE;
		while (processingPilotEvents) do
			pilotEventID = getNextPilotEvent(pilotEventParams);
			if (pilotEventID == PILOT_EVENT_NONE) then
				processingPilotEvents = FALSE;
			else
				switch (pilotEventID)
					case PILOT_EVENT_TARGETED:
						curTarget = getTarget(-1);
						if (lastHelpRequestTime < (curTime - helpRequestFrequency)) then
							lastHelpRequestTime = curTime;
							if (willRequestHelp) then
								//distanceToAttacker = distanceToObject(-1, pilotEventParams[0]);
								getObjectPosition(pilotEventParams[0], attackerPos);
								getObjectPosition(-1, myPos);
								requestHelp(pilotEventParams[0], myPos, attackerHelpRadius, attackerPos, defenderHelpRadius, 1);
							endif;
						endif;
						numFunctionalWeapons = getWeapons(weaponList, 0);
						if (curStateHandle <> AttackStateHandle) then
							if ((numFunctionalWeapons > 0) and (curTarget == 0)) then
								coreAttack(pilotEventParams[0], TACORDER_PARAM_PURSUE);
								setState(AttackStateHandle);
							endif;
						endif;
						endcase;
					case PILOT_EVENT_ATTACK_ORDER:
						curTarget = getTarget(-1);
						if (curStateHandle <> AttackStateHandle) then
							if ((numFunctionalWeapons > 0) and (curTarget == 0))then
								coreAttack(pilotEventParams[0], TACORDER_PARAM_PURSUE);
								setState(AttackStateHandle);
							endif;
						endif;
						endcase;
					case PILOT_EVENT_FIRED_WEAPON:
						endcase;
				endswitch;
			endif;
		endwhile;

		return(0);

endfunction;

//----------------------------------------------------------------------------------------

state noWeapons;

	code

		setDebugString(-1, 3, "   NO WEAPONS ");
		if (objectClass(-1) == 2) then
			coreEject;
		else
			corePower(false);
		endif;
		
endstate;

//----------------------------------------------------------------------------------------

state attack;

	var
		integer		tacticState;

	code

		update;
		setDebugString(-1, 3, " ATTACK    ");
		//coreAttackTactic(0, TACORDER_PARAM_NONE, TACTIC_JOUST, tacticState);
		coreAttack(0, TACORDER_PARAM_PURSUE);
		resetOrders(1);
		transBack;

endstate;

//----------------------------------------------------------------------------------------

state attackingBase;

	code

		if ((getTime > TurnOffSensor)) then
			setTargetPriority(0, TARGET_PRIORITY_CURTARGET, -1, 500, CONTACT_CRITERIA_ENEMY + CONTACT_CRITERIA_VISUAL_OR_SENSOR + CONTACT_CRITERIA_NOT_DISABLED);
			setTargetPriority(1, TARGET_PRIORITY_MOVER, 0, 500, CONTACT_CRITERIA_ENEMY + CONTACT_CRITERIA_VISUAL_OR_SENSOR + CONTACT_CRITERIA_NOT_DISABLED);
		endif;	

		update;

		setDebugString(-1, 3, "attackingBase ");	
		corePatrol(startPatrolState, startPatrolPath, attackStateHandle);
		resetOrders(1);
		trans PatrolBase;
	
endstate;

//----------------------------------------------------------------------------------------

state PatrolBase;

	code

		update;

		setDebugString(-1, 3, "PatrolBase ");	
		corePatrol(basePatrolState, basePatrolPath, attackStateHandle);
		resetOrders(1);

	
endstate;

//----------------------------------------------------------------------------------------

state WaitForObj1;

	var
		worldPosition		WaitGuard;	

	code

		WaitGuard[0] = -7274;
		WaitGuard[1] = -362;
		waitGuard[2] = 0;

		update;

		setDebugString(-1, 3, " WaitObj1 ");	
		coreGuard(waitGuard, -1, attackStateHandle);
		resetOrders(1);
	
endstate;

//----------------------------------------------------------------------------------------

state start;

	code

		update;

endstate;

//----------------------------------------------------------------------------------------

endfsm.

//****************************************************************************************
