//****************************************************************************************//
//
//		Brains used to for HillTop personal guards
//
//      MISSION:  Mc2_17
//
//
//****************************************************************************************//

fsm mc2_17_EC1_Stein_Guard_2;

	var

		static WorldPosition		startPosition;
		static PatrolState		retreatPatrolState;
		static PatrolPath			retreatPatrolPath;
		static boolean			willRequestHelp;
		static real				lastHelpRequestTime;
		static real				helpRequestFrequency;
		static real				attackerHelpRadius;
		static real				defenderHelpRadius;
		static integer			AttackStateHandle;

		static boolean			poweredDown;


		static integer			numFunctionalWeapons;
		static integer[20]		weaponList;
		static boolean			BeginFlankMove;
		
		static boolean			goRetreating;

//****************************************************************************************

function init;

    code

		//setDebugWindow(-1, -1);

		//---------------------------
		// Grab his start position...
		getObjectPosition(-1, startPosition);

		//-------------------
		// Look for movers...
		setTargetPriority(0, TARGET_PRIORITY_CURTARGET, -1, 150, CONTACT_CRITERIA_ENEMY + CONTACT_CRITERIA_VISUAL_OR_SENSOR + CONTACT_CRITERIA_NOT_DISABLED);
		setTargetPriority(1, TARGET_PRIORITY_MOVER, 0, 300, CONTACT_CRITERIA_ENEMY + CONTACT_CRITERIA_VISUAL_OR_SENSOR + CONTACT_CRITERIA_NOT_DISABLED);
		setTargetPriority(2, TARGET_PRIORITY_NONE, 0, 0, 0);

		AttackStateHandle = getStateHandle("attack");
		BeginFlankMove = False;
		goRetreating = False;

		retreatPatrolState[0] = PATROL_TYPE_LINEAR;
		retreatPatrolState[1] = 2;    //?num points
		retreatPatrolState[2] = 1;    //?num cycles
		retreatPatrolState[3] = PATROL_DIRECTION_FORWARD;
		retreatPatrolState[4] = -1;    //reset cur point
		retreatPatrolState[5] = -1;    //reset cur cycle
		retreatPatrolState[6] = CONTACT_CRITERIA_ENEMY + CONTACT_CRITERIA_VISUAL_OR_SENSOR + CONTACT_CRITERIA_NOT_DISABLED;
		retreatPatrolPath[0, 0] = -6636;
		retreatPatrolPath[0, 1] = -661;
		retreatPatrolPath[1, 0] = -7189;
		retreatPatrolPath[1, 1] = -405 + 44;


		willRequestHelp = true; //?true or false
		helpRequestFrequency = 20.0; //?in secs
		attackerHelpRadius = 100; //?in meters
		defenderHelpRadius = 125; //?in meters
		lastHelpRequestTime = -100.0;
						
endfunction;


//----------------------------------------------------------------------------------------

function setWillRequestHelp (boolean setting);

	code

		if (setting and (not willRequestHelp)) then
			lastHelpRequestTime = 0.0;
		endif;
		willRequestHelp = setting;

endfunction;

//----------------------------------------------------------------------------------------

function update : integer;

	var

		boolean		processingPilotEvents;
		boolean		thinking;
		integer		pilotEventID;
		integer		pilotState;
		integer[20]		pilotEventParams;
		integer		curTarget;
		real			curTime;
		real[3]		myPos;
		real[3]		attackerPos;
		real			distanceToAttacker;
		integer		curStateHandle;


	code

		curTime = getTime;
		curStateHandle = getCurrentStateHandle;


		//--------------------------------------------------
		// Process the pilot events since the last update...
		numFunctionalWeapons = getWeapons(weaponList, 1);
		if (numFunctionalWeapons == 0) then
			trans noWeapons;
		endif;

		//--------------------------------------------------
		// Check to see if time to attack the player
		if (liaoBetrays) and (not BeginFlankMove) then
			BeginFlankMove = True;
			trans GuardHill;
		endif;
			
		//--------------------------------------------------
		// Check to see if time to attack the player
		if (LiaoSonAttack) and (not goRetreating) then
			goRetreating = True;
			trans retreatOffMap;
		endif;
			

		processingPilotEvents = TRUE;
		while (processingPilotEvents) do
			pilotEventID = getNextPilotEvent(pilotEventParams);
			if (pilotEventID == PILOT_EVENT_NONE) then
				processingPilotEvents = FALSE;
			else
				switch (pilotEventID)
					case PILOT_EVENT_TARGETED:
						curTarget = getTarget(-1);
						if (lastHelpRequestTime < (curTime - helpRequestFrequency)) then
							lastHelpRequestTime = curTime;
							if (willRequestHelp) then
								//distanceToAttacker = distanceToObject(-1, pilotEventParams[0]);
								getObjectPosition(pilotEventParams[0], attackerPos);
								getObjectPosition(-1, myPos);
								requestHelp(pilotEventParams[0], myPos, attackerHelpRadius, attackerPos, defenderHelpRadius, 1);
							endif;
						endif;
						numFunctionalWeapons = getWeapons(weaponList, 0);
						if (curStateHandle <> AttackStateHandle) then
							if ((numFunctionalWeapons > 0) and (curTarget == 0)) then
								coreAttack(pilotEventParams[0], TACORDER_PARAM_PURSUE);
								setState(AttackStateHandle);
							endif;
						endif;
						endcase;
					case PILOT_EVENT_ATTACK_ORDER:
						curTarget = getTarget(-1);
						if (curStateHandle <> AttackStateHandle) then
							if ((numFunctionalWeapons > 0) and (curTarget == 0))then
								coreAttack(pilotEventParams[0], TACORDER_PARAM_PURSUE);
								setState(AttackStateHandle);
							endif;
						endif;
						endcase;
					case PILOT_EVENT_FIRED_WEAPON:
						endcase;
				endswitch;
			endif;
		endwhile;

		return(0);

endfunction;

//----------------------------------------------------------------------------------------

state noWeapons;

	code

		setDebugString(-1, 3, "   NO WEAPONS ");
		if (objectClass(-1) == 2) then
			coreEject;
		else
			corePower(false);
		endif;
		
endstate;

//----------------------------------------------------------------------------------------

state attack;

	var
		integer		tacticState;

	code

		update;
		setDebugString(-1, 3, " ATTACK    ");
		coreAttack(0, TACORDER_PARAM_PURSUE);
		resetOrders(1);
		transBack;

endstate;

//----------------------------------------------------------------------------------------

state GuardHill;

	var
		worldPosition		guardSpot;

	code

		guardSpot[0] = -5056.000;
		guardSpot[1] = -1002.000;
		guardSpot[2] = 0;

		update;

		setDebugString(-1, 3, " GuardingPass ");	
		//corePatrol(startPatrolState, startPatrolPath, attackstateHandle);
		coreGuard(guardSpot, -1, attackStateHandle);
		resetOrders(1);
	
endstate;

//----------------------------------------------------------------------------------------

state retreatOffMap;


	code

		update;

		setDebugString(-1, 3, "retreatOffMap");	
		corePatrol(retreatPatrolState, retreatPatrolPath, attackstateHandle);
		resetOrders(1);
		objectRemove(getID);
		
	
endstate;

//----------------------------------------------------------------------------------------

state start;

	code

		update;

endstate;

//----------------------------------------------------------------------------------------

endfsm.

//****************************************************************************************
