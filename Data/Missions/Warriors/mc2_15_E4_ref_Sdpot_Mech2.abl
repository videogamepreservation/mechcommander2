//****************************************************************************************//
// 
//		Ecounter 4 Mech that patrol Production Fac and Reinforce the Supply Depot
//
//      MISSION:  MC2_15
//			Bushwacker
//		This unit will scan for the player. And attack the player if detected
//
//****************************************************************************************//

fsm mc2_15_E4_ref_Sdpot_Mech2;

	var

		static WorldPosition		startPosition;
		static PatrolState		startPatrolState, SupplyBase_AttackState, SupplyBase_PatrolState;
		static PatrolPath			startPatrolPath, SupplyBase_AttackPatrol, SupplyBase_PatrolPath;
		static boolean			willRequestHelp;
		static real				lastHelpRequestTime;
		static real				helpRequestFrequency;
		static real				attackerHelpRadius;
		static real				defenderHelpRadius;
		static integer			AttackStateHandle;

		static integer			numFunctionalWeapons;
		static integer[20]		weaponList;

		static boolean			supplyPatrolTrigger;

//****************************************************************************************

function init;

    code

		//setDebugWindow(-1, -1);

		//---------------------------
		// Grab his start position...
		getObjectPosition(-1, startPosition);

		//-------------------
		// Look for movers...
		setTargetPriority(0, TARGET_PRIORITY_CURTARGET, -1, 150, CONTACT_CRITERIA_ENEMY + CONTACT_CRITERIA_VISUAL_OR_SENSOR + CONTACT_CRITERIA_NOT_DISABLED);
		setTargetPriority(1, TARGET_PRIORITY_MOVER, 0, 400, CONTACT_CRITERIA_ENEMY + CONTACT_CRITERIA_VISUAL_OR_SENSOR + CONTACT_CRITERIA_NOT_DISABLED);
		setTargetPriority(2, TARGET_PRIORITY_NONE, 0, 0, 0);

		AttackStateHandle = getStateHandle("attack");
		supplyPatrolTrigger = false;

		SupplyBase_PatrolState[0] = PATROL_TYPE_LINEAR;    
		SupplyBase_PatrolState[1] = 2;    //?num points
		SupplyBase_PatrolState[2] = -1;    //?num cycles
		SupplyBase_PatrolState[3] = PATROL_DIRECTION_FORWARD;    
		SupplyBase_PatrolState[4] = -1;    //reset cur point
		SupplyBase_PatrolState[5] = -1;    //reset cur cycle
		SupplyBase_PatrolState[6] = CONTACT_CRITERIA_ENEMY + CONTACT_CRITERIA_VISUAL_OR_SENSOR + CONTACT_CRITERIA_NOT_DISABLED;    
		SupplyBase_PatrolPath[0, 0] = 3562.6667;
		SupplyBase_PatrolPath[0, 1] = -832.000;
		SupplyBase_PatrolPath[1, 0] = 490.667;
		SupplyBase_PatrolPath[1, 1] = -5525.333;

		SupplyBase_AttackState[0] = PATROL_TYPE_LINEAR;    
		SupplyBase_AttackState[1] = 2;    //?num points
		SupplyBase_AttackState[2] = 1;    //?num cycles
		SupplyBase_AttackState[3] = PATROL_DIRECTION_FORWARD;    
		SupplyBase_AttackState[4] = -1;    //reset cur point
		SupplyBase_AttackState[5] = -1;    //reset cur cycle
		SupplyBase_AttackState[6] = CONTACT_CRITERIA_ENEMY + CONTACT_CRITERIA_VISUAL_OR_SENSOR + CONTACT_CRITERIA_NOT_DISABLED;    
		SupplyBase_AttackPatrol[0, 0] = 3562.6667;
		SupplyBase_AttackPatrol[0, 1] = -832.000;
		SupplyBase_AttackPatrol[1, 0] = 2624.000;
		SupplyBase_AttackPatrol[1, 1] = -832.000;

		//-------------------------------------------	
		// Setup the Patrol here...	
		//XL startpatrolState Start	

		startpatrolState[0] = PATROL_TYPE_LINEAR;    	
		startpatrolState[1] = 8;    //?num points	
		startpatrolState[2] = -1;    //?num cycles	
		startpatrolState[3] = PATROL_DIRECTION_FORWARD;    	
		startpatrolState[4] = -1;    //reset cur point	
		startpatrolState[5] = -1;    //reset cur cycle	
		startpatrolState[6] = 0 + CONTACT_CRITERIA_ENEMY + CONTACT_CRITERIA_VISUAL + CONTACT_CRITERIA_NOT_DISABLED;    	

		startPatrolPath[0, 0] = -533.000;	
		startPatrolPath[0, 1] = 2794.000 + 44;	
		startPatrolPath[1, 0] = 1045.000;	
		startPatrolPath[1, 1] = 2538.000 + 44;	
		startPatrolPath[2, 0] = 1514.000;	
		startPatrolPath[2, 1] = 1861.000 + 44;	
		startPatrolPath[3, 0] = 1301.000;	
		startPatrolPath[3, 1] = 5141.000 + 44;	
		startPatrolPath[4, 0] = 405.000;	
		startPatrolPath[4, 1] = 5909.000 + 44;	
		startPatrolPath[5, 0] = -789.000;	
		startPatrolPath[5, 1] = 5866.000 + 44;	
		startPatrolPath[6, 0] = -1045.000;	
		startPatrolPath[6, 1] = 4373.000 + 44;	
		startPatrolPath[7, 0] = -1258.667;	
		startPatrolPath[7, 1] = 3178.667 + 44;	
			
			
		//XLs startPatrolPath end


		willRequestHelp = true; //?true or false
		helpRequestFrequency = 20.0; //?in secs
		attackerHelpRadius = 200; //?in meters
		defenderHelpRadius = 225; //?in meters
		lastHelpRequestTime = -100.0;
						
endfunction;


//----------------------------------------------------------------------------------------

function setWillRequestHelp (boolean setting);

	code

		if (setting and (not willRequestHelp)) then
			lastHelpRequestTime = 0.0;
		endif;
		willRequestHelp = setting;

endfunction;

//----------------------------------------------------------------------------------------

function update : integer;

	var

		boolean		processingPilotEvents;
		boolean		thinking;
		integer		pilotEventID;
		integer		pilotState;
		integer[20]		pilotEventParams;
		integer		curTarget;
		real			curTime;
		real[3]		myPos;
		real[3]		attackerPos;
		real			distanceToAttacker;
		integer		curStateHandle;


	code
		//--------------------------------------------------
		// Check Time and Check Current State Handle
		curTime = getTime;
		curStateHandle = getCurrentStateHandle;
		//--------------------------------------------------


		//--------------------------------------------------
		// Trigger for when Supply base gets attacked.
		if (supplyBase_Attacked) and (Not supplyPatrolTrigger) then
			supplyPatrolTrigger = True;
			trans SupplyBaseAttacked;
		endif;

		//--------------------------------------------------
		// Process the pilot events since the last update...
		numFunctionalWeapons = getWeapons(weaponList, 1);
		if (numFunctionalWeapons == 0) then
			trans noWeapons;
		endif;

		processingPilotEvents = TRUE;
		while (processingPilotEvents) do
			pilotEventID = getNextPilotEvent(pilotEventParams);
			if (pilotEventID == PILOT_EVENT_NONE) then
				processingPilotEvents = FALSE;
			else
				switch (pilotEventID)
					case PILOT_EVENT_TARGETED:
						curTarget = getTarget(-1);
						if (lastHelpRequestTime < (curTime - helpRequestFrequency)) then
							lastHelpRequestTime = curTime;
							if (willRequestHelp) then
								//distanceToAttacker = distanceToObject(-1, pilotEventParams[0]);
								getObjectPosition(pilotEventParams[0], attackerPos);
								getObjectPosition(-1, myPos);
								requestHelp(pilotEventParams[0], myPos, attackerHelpRadius, attackerPos, defenderHelpRadius, 1);
							endif;
						endif;
						numFunctionalWeapons = getWeapons(weaponList, 0);
						if (curStateHandle <> AttackStateHandle) then
							if ((numFunctionalWeapons > 0) and (curTarget == 0)) then
								coreAttack(pilotEventParams[0], TACORDER_PARAM_PURSUE);
								setState(AttackStateHandle);
							endif;
						endif;
						endcase;
					case PILOT_EVENT_ATTACK_ORDER:
						curTarget = getTarget(-1);
						if (curStateHandle <> AttackStateHandle) then
							if ((numFunctionalWeapons > 0) and (curTarget == 0))then
								coreAttack(pilotEventParams[0], TACORDER_PARAM_PURSUE);
								setState(AttackStateHandle);
							endif;
						endif;
						endcase;
					case PILOT_EVENT_FIRED_WEAPON:
						endcase;
				endswitch;
			endif;
		endwhile;

		return(0);

endfunction;

//----------------------------------------------------------------------------------------

state noWeapons;

	code

		setDebugString(-1, 3, "   NO WEAPONS ");
		if (objectClass(-1) == 2) then
			coreEject;
		else
			corePower(false);
		endif;
		
endstate;

//----------------------------------------------------------------------------------------

state attack;
	var

		integer		tacticState;
	code

		update;
		setDebugString(-1, 3, " ATTACK    ");
		coreAttackTactic(0, TACORDER_PARAM_NONE, TACTIC_RIGHT_FLANK, tacticState);
//		coreAttack(0, TACORDER_PARAM_PURSUE);
		resetOrders(1);
		transBack;

endstate;

//----------------------------------------------------------------------------------------
state SupplyBaseAttacked;

	code
		update;
		setDebugString(-1, 3, " ReinForceAtt    ");
		//coreRun = True;
		corePatrol(SupplyBase_AttackState, SupplyBase_AttackPatrol, AttackStateHandle);
		trans SupplyBasePatrol;
endstate;

//----------------------------------------------------------------------------------------
state SupplyBasePatrol;

	code
		update;
		setDebugString(-1, 3, " ReinForce    ");	
		corePatrol(SupplyBase_PatrolState, SupplyBase_PatrolPath, AttackStateHandle);
		resetOrders(1);
endstate;

//----------------------------------------------------------------------------------------

state start;

	code

		update;
		setDebugString(-1, 3, "StartPatrol1     ");
		//coreGuard(startPosition, -1 , AttackStateHandle);
		corePatrol(startPatrolState, startPatrolPath, AttackStateHandle);
		resetOrders(1);
endstate;

//----------------------------------------------------------------------------------------

endfsm.

//****************************************************************************************
