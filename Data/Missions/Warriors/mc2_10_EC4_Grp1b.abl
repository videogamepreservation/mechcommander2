//****************************************************************************************//
//
//		Ecounter 4 Group 1
//
//      MISSION:  Mc2_10
//		This is 1st of the Reinforcement Groups
//		
//
//****************************************************************************************//

fsm mc2_10_EC4_Grp1b;

	var

		static WorldPosition		startPosition;
		static PatrolState		gulagPatrolState;
		static PatrolPath			gulagPatrolPath;
		static boolean			willRequestHelp;
		static real				lastHelpRequestTime;
		static real				helpRequestFrequency;
		static real				attackerHelpRadius;
		static real				defenderHelpRadius;
		static integer			AttackStateHandle;

		static real				goToGulagTimer;
		static Boolean			TestGoGulag, TimerGoGulag;

		static integer			numFunctionalWeapons;
		static integer[20]		weaponList;

//****************************************************************************************

function init;

    code

		//setDebugWindow(-1, -1);

		//---------------------------
		// Grab his start position...
		getObjectPosition(-1, startPosition);

		//-------------------
		// Look for movers...
		setTargetPriority(0, TARGET_PRIORITY_CURTARGET, -1, 100, CONTACT_CRITERIA_ENEMY + CONTACT_CRITERIA_VISUAL_OR_SENSOR + CONTACT_CRITERIA_NOT_DISABLED); //PLACEHOLDER
		setTargetPriority(1, TARGET_PRIORITY_MOVER, 0, 300, CONTACT_CRITERIA_ENEMY + CONTACT_CRITERIA_VISUAL_OR_SENSOR + CONTACT_CRITERIA_NOT_DISABLED); //PLACEHOLDER
		setTargetPriority(2, TARGET_PRIORITY_BUILDING, 0, 100, -1);

		AttackStateHandle = getStateHandle("attack");
		TestgoGulag = False;
		TimerGoGulag = False;


		//-------------------------------------------
		// Setup the Patrol here...
		//XL gulagPatrolState Start


		gulagPatrolState[0] = PATROL_TYPE_LINEAR;
		gulagPatrolState[1] = 4;    //?num points
		gulagPatrolState[2] = 1;    //?num cycles
		gulagPatrolState[3] = PATROL_DIRECTION_FORWARD;
		gulagPatrolState[4] = -1;    //reset cur point
		gulagPatrolState[5] = -1;    //reset cur cycle
		gulagPatrolState[6] = 0 + CONTACT_CRITERIA_ENEMY + CONTACT_CRITERIA_VISUAL + CONTACT_CRITERIA_NOT_DISABLED;

		gulagPatrolPath[0, 0] = 2496.000;
		gulagPatrolPath[0, 1] = 3861.333;
		gulagPatrolPath[1, 0] = -746.667;
		gulagPatrolPath[1, 1] = -192.000;
		gulagPatrolPath[2, 0] = -1728.000;
		gulagPatrolPath[2, 1] = 618.667;
		gulagPatrolPath[3, 0] = -1728.000;
		gulagPatrolPath[3, 1] = 2122.000;

/*  		DEBUG

		gulagPatrolState[0] = PATROL_TYPE_LINEAR;
		gulagPatrolState[1] = 11;    //?num points
		gulagPatrolState[2] = 1;    //?num cycles
		gulagPatrolState[3] = PATROL_DIRECTION_FORWARD;
		gulagPatrolState[4] = -1;    //reset cur point
		gulagPatrolState[5] = -1;    //reset cur cycle
		gulagPatrolState[6] = 0 + CONTACT_CRITERIA_ENEMY + CONTACT_CRITERIA_VISUAL + CONTACT_CRITERIA_NOT_DISABLED;

		gulagPatrolPath[0, 0] = 2922.667;
		gulagPatrolPath[0, 1] = 4672.000;
		gulagPatrolPath[1, 0] = 2880.000;
		gulagPatrolPath[1, 1] = 3904.000;
		gulagPatrolPath[2, 0] = 2496.000;
		gulagPatrolPath[2, 1] = 3904.000;
		gulagPatrolPath[3, 0] = 2496.000;
		gulagPatrolPath[3, 1] = 2538.667;
		gulagPatrolPath[4, 0] = 1770.667;
		gulagPatrolPath[4, 1] = 2496.000;
		gulagPatrolPath[5, 0] = 1728.000;
		gulagPatrolPath[5, 1] = 1642.667;
		gulagPatrolPath[6, 0] = 960.000;
		gulagPatrolPath[6, 1] = 1557.333;
		gulagPatrolPath[7, 0] = 960.000;
		gulagPatrolPath[7, 1] = 1002.667;
		gulagPatrolPath[8, 0] = 192.000;
		gulagPatrolPath[8, 1] = 960.000;
		gulagPatrolPath[9, 0] = 192.000;
		gulagPatrolPath[9, 1] = -192.000;
		gulagPatrolPath[10, 0] = -1728.000;
		gulagPatrolPath[10, 1] = -149.000;
		
		DEBUG */

		//XLs gulagPatrolPath end


		willRequestHelp = true; //?true or false
		helpRequestFrequency = 20.0; //?in secs
		attackerHelpRadius = 200; //?in meters
		defenderHelpRadius = 225; //?in meters
		lastHelpRequestTime = -100.0;
						
endfunction;


//----------------------------------------------------------------------------------------

function setWillRequestHelp (boolean setting);

	code

		if (setting and (not willRequestHelp)) then
			lastHelpRequestTime = 0.0;
		endif;
		willRequestHelp = setting;

endfunction;

//----------------------------------------------------------------------------------------

function update : integer;

	var

		boolean		processingPilotEvents;
		boolean		thinking;
		integer		pilotEventID;
		integer		pilotState;
		integer[20]		pilotEventParams;
		integer		curTarget;
		real			curTime;
		real[3]		myPos;
		real[3]		attackerPos;
		real			distanceToAttacker;
		integer		curStateHandle;


	code

		curTime = getTime;
		curStateHandle = getCurrentStateHandle;

		//-----------------------------------
		// Trigger for to set the Timer for Reinforcements to Attack the Gulag
		if (Reinforce_Go) then
			if (Not TestGoGulag) then
				TestGoGulag= true;
				goToGulagTimer = getTime + 1
			else
				if (Not TimerGoGulag) AND (getTime > goToGulagTimer) then
					TimerGoGulag  = True;
					trans toGulag;
				endif;
			endif;
		endif;

/*DEBUG	//-----------------------------------
		// Trigger for Reinforcements to Attack the Gulag
		if (Not TimerGoGulag) AND (getTime > goToGulagTimer) then
			TimerGoGulag  = True;
			trans toGulag;
		endif;
DEBUG*/

		//--------------------------------------------------
		// Process the pilot events since the last update...
		numFunctionalWeapons = getWeapons(weaponList, 1);
		if (numFunctionalWeapons == 0) then
			trans noWeapons;
		endif;

		processingPilotEvents = TRUE;
		while (processingPilotEvents) do
			pilotEventID = getNextPilotEvent(pilotEventParams);
			if (pilotEventID == PILOT_EVENT_NONE) then
				processingPilotEvents = FALSE;
			else
				switch (pilotEventID)
					case PILOT_EVENT_TARGETED:
						curTarget = getTarget(-1);
						if (lastHelpRequestTime < (curTime - helpRequestFrequency)) then
							lastHelpRequestTime = curTime;
							if (willRequestHelp) then
								//distanceToAttacker = distanceToObject(-1, pilotEventParams[0]);
								getObjectPosition(pilotEventParams[0], attackerPos);
								getObjectPosition(-1, myPos);
								requestHelp(pilotEventParams[0], myPos, attackerHelpRadius, attackerPos, defenderHelpRadius, 1);
							endif;
						endif;
						numFunctionalWeapons = getWeapons(weaponList, 0);
						if (curStateHandle <> AttackStateHandle) then
							if ((numFunctionalWeapons > 0) and (curTarget == 0)) then
								coreAttack(pilotEventParams[0], TACORDER_PARAM_PURSUE);
								setState(AttackStateHandle);
							endif;
						endif;
						endcase;
					case PILOT_EVENT_ATTACK_ORDER:
						curTarget = getTarget(-1);
						if (curStateHandle <> AttackStateHandle) then
							if ((numFunctionalWeapons > 0) and (curTarget == 0))then
								coreAttack(pilotEventParams[0], TACORDER_PARAM_PURSUE);
								setState(AttackStateHandle);
							endif;
						endif;
						endcase;
					case PILOT_EVENT_FIRED_WEAPON:
						endcase;
				endswitch;
			endif;
		endwhile;

		return(0);

endfunction;

//----------------------------------------------------------------------------------------

state noWeapons;

	code

		setDebugString(-1, 3, "   NO WEAPONS ");
		if (objectClass(-1) == 2) then
			coreEject;
		else
			corePower(false);
		endif;
		
endstate;

//----------------------------------------------------------------------------------------

state attack;

	var
		integer			tacticState;

	code

		update;
		setDebugString(-1, 3, " ATTACK    ");
		coreAttackTactic(0, TACORDER_PARAM_NONE, TACTIC_RIGHT_FLANK, tacticState);
		resetOrders(1);
		transBack;

endstate;


//----------------------------------------------------------------------------------------

state toGulag;
	
	var
		worldPosition	gulagGuard;

	code

		update;
		setDebugString(-1, 3, " ToGulag    ");
		corePatrol(gulagPatrolState, gulagPatrolPath, attackStateHandle);
		gulagGuard[0] = -1728.000;
		gulagGuard[1] = 2122.000;
		gulagGuard[2] = 0.0;
		coreGuard(gulagGuard, -1, AttackStateHandle);
		resetOrders(1);
		
endstate;
//----------------------------------------------------------------------------------------

state start;

	code

		update;
		setDebugString(-1, 3, " GUARD      ");
		coreGuard(startPosition, -1, AttackStateHandle);
		resetOrders(1);
endstate;

//----------------------------------------------------------------------------------------

endfsm.

//****************************************************************************************
