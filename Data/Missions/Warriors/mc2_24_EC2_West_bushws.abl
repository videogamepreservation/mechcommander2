//****************************************************************************************//
//
//		Ecounter 3 - Group of ???
//
//      MISSION:  Mc2_24
//		These Unit(s) GUARD the WEST OF THE BASE
//		
//
//****************************************************************************************//

fsm mc2_24_EC2_West_bushws;

	var

		static WorldPosition		startPosition;
		static PatrolState		startPatrolState;
		static PatrolPath			startPatrolPath;
		static boolean			willRequestHelp;
		static real				lastHelpRequestTime;
		static real				helpRequestFrequency;
		static real				attackerHelpRadius;
		static real				defenderHelpRadius;
		static integer			AttackStateHandle;

		static integer			numFunctionalWeapons;
		static integer[20]		weaponList;
		static PatrolState		startBase1PatrolState;
		static PatrolPath		startBase1PatrolPath;
		static PatrolState		startGetPlayerPatrolState;
		static PatrolPath		startGetPlayerPatrolPath;
		static boolean			AT_Check;
//****************************************************************************************

function init;

    code

		//setDebugWindow(-1, -1);

		//---------------------------
		// Grab his start position...
		getObjectPosition(-1, startPosition);

		//-------------------
		// Look for movers...
		setTargetPriority(0, TARGET_PRIORITY_CURTARGET, -1, 150, CONTACT_CRITERIA_ENEMY + CONTACT_CRITERIA_VISUAL_OR_SENSOR + CONTACT_CRITERIA_NOT_DISABLED);
		setTargetPriority(1, TARGET_PRIORITY_MOVER, 0, 350, CONTACT_CRITERIA_ENEMY + CONTACT_CRITERIA_VISUAL_OR_SENSOR + CONTACT_CRITERIA_NOT_DISABLED);
		setTargetPriority(2, TARGET_PRIORITY_NONE, 0, 0, 0);

		AttackStateHandle = getStateHandle("attack");

		AT_Check = false;
		
		startBase1PatrolState[0] = PATROL_TYPE_LINEAR;
		startBase1PatrolState[1] = 2;    //?num points
		startBase1PatrolState[2] = -1;    //?num cycles
		startBase1PatrolState[3] = PATROL_DIRECTION_FORWARD;
		startBase1PatrolState[4] = -1;    //reset cur point
		startBase1PatrolState[5] = -1;    //reset cur cycle
		startBase1PatrolState[6] = 0 + CONTACT_CRITERIA_ENEMY + CONTACT_CRITERIA_VISUAL + CONTACT_CRITERIA_NOT_DISABLED;

		startBase1PatrolPath[0, 0] = -4160.000;
		startBase1PatrolPath[0, 1] = 2624.000;
		startBase1PatrolPath[1, 0] = -3008.000;
		startBase1PatrolPath[1, 1] = 2752.000;

		
		startGetPlayerPatrolState[0] = PATROL_TYPE_LOOPING;
		startGetPlayerPatrolState[1] = 4;    //?num points
		startGetPlayerPatrolState[2] = -1;    //?num cycles
		startGetPlayerPatrolState[3] = PATROL_DIRECTION_FORWARD;
		startGetPlayerPatrolState[4] = -1;    //reset cur point
		startGetPlayerPatrolState[5] = -1;    //reset cur cycle
		startGetPlayerPatrolState[6] = 0 + CONTACT_CRITERIA_ENEMY + CONTACT_CRITERIA_VISUAL + CONTACT_CRITERIA_NOT_DISABLED;

		startGetPlayerPatrolPath[0, 0] = -1728.000;
		startGetPlayerPatrolPath[0, 1] = -2496.000;
		startGetPlayerPatrolPath[1, 0] = -1728.000;
		startGetPlayerPatrolPath[1, 1] = -4288.000;
		startGetPlayerPatrolPath[2, 0] = -1728.000;
		startGetPlayerPatrolPath[2, 1] = -2496.000;
		startGetPlayerPatrolPath[3, 0] = -3136.000;
		startGetPlayerPatrolPath[3, 1] = -2496.000;
		
		
		willRequestHelp = true; //?true or false
		helpRequestFrequency = 20.0; //?in secs
		attackerHelpRadius = 100; //?in meters
		defenderHelpRadius = 125; //?in meters
		lastHelpRequestTime = -100.0;
						
endfunction;


//----------------------------------------------------------------------------------------

function setWillRequestHelp (boolean setting);

	code

		if (setting and (not willRequestHelp)) then
			lastHelpRequestTime = 0.0;
		endif;
		willRequestHelp = setting;

endfunction;

//----------------------------------------------------------------------------------------

function update : integer;

	var

		boolean		processingPilotEvents;
		boolean		thinking;
		integer		pilotEventID;
		integer		pilotState;
		integer[20]		pilotEventParams;
		integer		curTarget;
		real			curTime;
		real[3]		myPos;
		real[3]		attackerPos;
		real			distanceToAttacker;
		integer		curStateHandle;


	code

		curTime = getTime;

		//-----------------------------------
		//
		curStateHandle = getCurrentStateHandle;


		// Trigger for East Area Trigger
		if (East_Area_Triggered) then
				if (NOT AT_Check) then
					AT_Check = True;
					trans getplayer;
				endif;
		endif;
		
		//--------------------------------------------------
		// Process the pilot events since the last update...
		numFunctionalWeapons = getWeapons(weaponList, 1);
		if (numFunctionalWeapons == 0) then
			trans noWeapons;
		endif;

		processingPilotEvents = TRUE;
		while (processingPilotEvents) do
			pilotEventID = getNextPilotEvent(pilotEventParams);
			if (pilotEventID == PILOT_EVENT_NONE) then
				processingPilotEvents = FALSE;
			else
				switch (pilotEventID)
					case PILOT_EVENT_TARGETED:
						curTarget = getTarget(-1);
						if (lastHelpRequestTime < (curTime - helpRequestFrequency)) then
							lastHelpRequestTime = curTime;
							if (willRequestHelp) then
								//distanceToAttacker = distanceToObject(-1, pilotEventParams[0]);
								getObjectPosition(pilotEventParams[0], attackerPos);
								getObjectPosition(-1, myPos);
								requestHelp(pilotEventParams[0], myPos, attackerHelpRadius, attackerPos, defenderHelpRadius, 1);
							endif;
						endif;
						numFunctionalWeapons = getWeapons(weaponList, 0);
						if (curStateHandle <> AttackStateHandle) then
							if ((numFunctionalWeapons > 0) and (curTarget == 0)) then
								coreAttack(pilotEventParams[0], TACORDER_PARAM_PURSUE);
								setState(AttackStateHandle);
							endif;
						endif;
						endcase;
					case PILOT_EVENT_ATTACK_ORDER:
						curTarget = getTarget(-1);
						if (curStateHandle <> AttackStateHandle) then
							if ((numFunctionalWeapons > 0) and (curTarget == 0))then
								coreAttack(pilotEventParams[0], TACORDER_PARAM_PURSUE);
								setState(AttackStateHandle);
							endif;
						endif;
						endcase;
					case PILOT_EVENT_FIRED_WEAPON:
						endcase;
				endswitch;
			endif;
		endwhile;

		return(0);

endfunction;

//----------------------------------------------------------------------------------------

state noWeapons;

	code

		setDebugString(-1, 3, "   NO WEAPONS ");
		if (objectClass(-1) == 2) then
			coreEject;
		else
			corePower(false);
		endif;
		
endstate;

//----------------------------------------------------------------------------------------

state getplayer;
	var

		integer		tacticState;

	code
		
		
		update;
		setDebugString(-1, 3, " GetPlayer    ");
		//coreRun = True;
		corePatrol(startGetPlayerPatrolState, startGetPlayerPatrolPath, AttackStateHandle);
		resetOrders(1);
		trans start;
		
endstate;


//----------------------------------------------------------------------------------------

state attack;

	var

		integer		tacticState;

	code

		update;
		setDebugString(-1, 3, " ATTACK    ");
		coreAttack(0, TACORDER_PARAM_PURSUE);
		resetOrders(1);
		transBack;

endstate;

//----------------------------------------------------------------------------------------

state start;

	code

		update;
		setDebugString(-1, 3, "  StartPatrol1     ");
		corePatrol(startBase1PatrolState, startBase1PatrolPath, AttackStateHandle);
		resetOrders(1);

endstate;

//----------------------------------------------------------------------------------------

endfsm.

//****************************************************************************************
