//****************************************************************************************//
//
//		Brain used Rebel Troops
//			
//      MISSION:  Mc2_17
//
//
//****************************************************************************************//

fsm mc2_17_rebel_RpTruck;

	var

		static WorldPosition		startPosition;
		static PatrolState		startPatrolState,  BasePatrolState;
		static PatrolPath			startPatrolPath,  BasePatrolPath;
		static boolean			willRequestHelp;
		static real				lastHelpRequestTime;
		static real				helpRequestFrequency;
		static real				attackerHelpRadius;
		static real				defenderHelpRadius;
		static integer			AttackStateHandle;

		static boolean			poweredDown;

		static integer			numFunctionalWeapons;
		static integer[20]		weaponList;
		static boolean			spawnRebels;
		static integer			StCounter;



//****************************************************************************************

function init;

    code

		//setDebugWindow(-1, -1);

		//---------------------------
		// Grab his start position...
		getObjectPosition(-1, startPosition);

		//-------------------
		// Look for movers...
		setTargetPriority(0, TARGET_PRIORITY_CURTARGET, -1, 0, CONTACT_CRITERIA_ENEMY + CONTACT_CRITERIA_VISUAL_OR_SENSOR + CONTACT_CRITERIA_NOT_DISABLED);
		setTargetPriority(1, TARGET_PRIORITY_MOVER, 0, 0, CONTACT_CRITERIA_ENEMY + CONTACT_CRITERIA_VISUAL_OR_SENSOR + CONTACT_CRITERIA_NOT_DISABLED);




		AttackStateHandle = getStateHandle("attack");
		spawnRebels = False;
		StCounter = 0;

		startPatrolState[0] = PATROL_TYPE_LINEAR;
		startPatrolState[1] = 3;    //?num points
		startPatrolState[2] = 1;    //?num cycles
		startPatrolState[3] = PATROL_DIRECTION_FORWARD;
		startPatrolState[4] = -1;    //reset cur point
		startPatrolState[5] = -1;    //reset cur cycle
		startPatrolState[6] = CONTACT_CRITERIA_ENEMY + CONTACT_CRITERIA_VISUAL_OR_SENSOR + CONTACT_CRITERIA_NOT_DISABLED;
		startPatrolPath[0, 0] = startPosition[0];
		startPatrolPath[0, 1] = startPosition[1];
		startPatrolPath[1, 0] = startPosition[0];
		startPatrolPath[1, 1] = startPosition[1] - 500;
		startPatrolPath[2, 0] = startPosition[0] - 810;
		startPatrolPath[2, 1] = startPosition[1] - 1110;



		willRequestHelp = true; //?true or false
		helpRequestFrequency = 20.0; //?in secs
		attackerHelpRadius = 100; //?in meters
		defenderHelpRadius = 125; //?in meters
		lastHelpRequestTime = -100.0;

		setWillHelp(True);
						
endfunction;


//----------------------------------------------------------------------------------------

function setWillRequestHelp (boolean setting);

	code

		if (setting and (not willRequestHelp)) then
			lastHelpRequestTime = 0.0;
		endif;
		willRequestHelp = setting;

endfunction;

//----------------------------------------------------------------------------------------

function update : integer;

	var

		boolean		processingPilotEvents;
		boolean		thinking;
		integer		pilotEventID;
		integer		pilotState;
		integer[20]		pilotEventParams;
		integer		curTarget;
		real			curTime;
		real[3]		myPos;
		real[3]		attackerPos;
		real			distanceToAttacker;
		integer		curStateHandle;


	code

		curTime = getTime;
		curStateHandle = getCurrentStateHandle;



		//--------------------------------------------------
		// Process the pilot events since the last update...
		numFunctionalWeapons = getWeapons(weaponList, 1);
		if (numFunctionalWeapons == 0) then
			//trans noWeapons;
		endif;




		//--------------------------------------------------
		// Check to see if time to guard base
		if (goRebels) and (Not spawnRebels) then
			spawnRebels = True;
			trans enterTheRebels;
		endif;

/*DEBUG	//--------------------------------------------------
		// Call Support
		if (STCounter < 5) and (LiaoReinForce_2) then
			StCounter = StCounter + 1;
			callStrike(1, 0, 2026.0, -1429.0, 0.0, true);
		endif;
*/


		processingPilotEvents = TRUE;
		while (processingPilotEvents) do
			pilotEventID = getNextPilotEvent(pilotEventParams);
			if (pilotEventID == PILOT_EVENT_NONE) then
				processingPilotEvents = FALSE;
			else
				switch (pilotEventID)
					case PILOT_EVENT_TARGETED:
						curTarget = getTarget(-1);
						if (lastHelpRequestTime < (curTime - helpRequestFrequency)) then
							lastHelpRequestTime = curTime;
							if (willRequestHelp) then
								//distanceToAttacker = distanceToObject(-1, pilotEventParams[0]);
								getObjectPosition(pilotEventParams[0], attackerPos);
								getObjectPosition(-1, myPos);
								requestHelp(pilotEventParams[0], myPos, attackerHelpRadius, attackerPos, defenderHelpRadius, 1);
							endif;
						endif;
						endcase;
					case PILOT_EVENT_ATTACK_ORDER:
						endcase;
					case PILOT_EVENT_FIRED_WEAPON:
						endcase;
				endswitch;
			endif;
		endwhile;

		return(0);

endfunction;

//----------------------------------------------------------------------------------------

state noWeapons;

	code

		setDebugString(-1, 3, "   NO WEAPONS ");
		if (objectClass(-1) == 2) then
			coreEject;
		else
			corePower(false);
		endif;
		
endstate;

//----------------------------------------------------------------------------------------

state attack;

	var
		integer		tacticState;
		worldposition		test;


	code

		update;
		setDebugString(-1, 3, " ATTACK    ");
		coreAttack(0, TACORDER_PARAM_TURRET);
//		coreAttackTactic(0, TACORDER_PARAM_NONE, TACTIC_FIRE_FROM_HERE, tacticState);
		//coreAttack(0, TACORDER_PARAM_PURSUE);
		resetOrders(1);
		transBack;

endstate;

//----------------------------------------------------------------------------------------

state enterTheRebels;

	code

		update;

		setDebugString(-1, 3, "enterTheRebels ");	
		corePatrol(startPatrolState, startPatrolPath, -1);
		trans GuardArea;
	
endstate;


//----------------------------------------------------------------------------------------

state GuardArea;

	var
		worldPosition		GuardSpot;

	code

		guardSpot[0] = startPatrolPath[2, 0];
		guardSpot[1] = startPatrolPath[2, 1];
		guardSpot[2] = 0;
		
		update;
		setDebugString(-1, 3, "GuardArea ");
		coreGuard(GuardSpot, -1, -1);
		resetOrders(1);

	
endstate;

//----------------------------------------------------------------------------------------

state start;

	code

		update;

endstate;

//----------------------------------------------------------------------------------------

endfsm.

//****************************************************************************************
