//****************************************************************************************//
// 
//		Ecounter 2 Group 2
//
//      MISSION:  Mc2_10
//		This Unit is the LRM Units on the Ridge.
//		This Unit Will not move from its' start position. 
//		But Will Switch to Engage the player.
//
//****************************************************************************************//

fsm mc2_10_EC2_Grp2;

	var

		static WorldPosition		startPosition;
		static PatrolState		startPatrolState;
		static PatrolPath			startPatrolPath;
		static boolean			willRequestHelp;
		static real				lastHelpRequestTime;
		static real				helpRequestFrequency;
		static real				attackerHelpRadius;
		static real				defenderHelpRadius;
		static integer			AttackStateHandle, StartStateHandle;

		static boolean			runAwayTest;
		static real				scanTime;
		static boolean			changeTarg;

		static integer			numFunctionalWeapons;
		static integer[20]		weaponList;
		static boolean			startAttack;


//****************************************************************************************

function init;

    code

		//setDebugWindow(-1, -1);

		//---------------------------
		// Grab his start position...
		getObjectPosition(-1, startPosition);

		//-------------------
		// Look for movers...
		setTargetPriority(0, TARGET_PRIORITY_CURTARGET, -1, 100, CONTACT_CRITERIA_ENEMY + CONTACT_CRITERIA_VISUAL_OR_SENSOR + CONTACT_CRITERIA_NOT_DISABLED);
		setTargetPriority(1, TARGET_PRIORITY_BUILDING, 0, 200, -1);
		setTargetPriority(2, TARGET_PRIORITY_NONE, 0, 0, 0);	

		AttackStateHandle = getStateHandle("attack");
		StartStateHandle = getStateHandle("start");
		runAwayTest = False;
		startAttack = False;

		startPatrolState[0] = PATROL_TYPE_LINEAR;    
		startPatrolState[1] = 1;    //?num points
		startPatrolState[2] = 1;    //?num cycles
		startPatrolState[3] = PATROL_DIRECTION_FORWARD;    
		startPatrolState[4] = -1;    //reset cur point
		startPatrolState[5] = -1;    //reset cur cycle
		startPatrolState[6] = CONTACT_CRITERIA_ENEMY + CONTACT_CRITERIA_VISUAL_OR_SENSOR + CONTACT_CRITERIA_NOT_DISABLED;    
		startPatrolPath[0, 0] = -4288.0;
		startPatrolPath[0, 1] = 2197.333;


		willRequestHelp = true; //?true or false
		helpRequestFrequency = 20.0; //?in secs
		attackerHelpRadius = 200; //?in meters
		defenderHelpRadius = 225; //?in meters
		lastHelpRequestTime = -100.0;
						
endfunction;


//----------------------------------------------------------------------------------------

function setWillRequestHelp (boolean setting);

	code

		if (setting and (not willRequestHelp)) then
			lastHelpRequestTime = 0.0;
		endif;
		willRequestHelp = setting;

endfunction;

//----------------------------------------------------------------------------------------

function update : integer;

	var

		boolean		processingPilotEvents;
		boolean		thinking;
		integer		pilotEventID;
		integer		pilotState;
		integer[20]		pilotEventParams;
		integer		curTarget;
		real			curTime;
		real[3]		myPos;
		real[3]		attackerPos;
		real			distanceToAttacker;
		integer		curStateHandle;


	code

		curTime = getTime;

		//-----------------------------------
		//
		curStateHandle = getCurrentStateHandle;



		if (ec2_attack_Gulag) then
			if (Not runAwayTest) then
				runAwayTest= true;
				//setTargetPriority(0, TARGET_PRIORITY_CURTARGET, -1, 400, CONTACT_CRITERIA_ENEMY + CONTACT_CRITERIA_VISUAL_OR_SENSOR + CONTACT_CRITERIA_NOT_DISABLED); //PLACEHOLDER
				//setTargetPriority(1, TARGET_PRIORITY_MOVER, 0, 300, CONTACT_CRITERIA_ENEMY + CONTACT_CRITERIA_VISUAL_OR_SENSOR + CONTACT_CRITERIA_NOT_DISABLED); //PLACEHOLDER
				setTargetPriority(1, TARGET_PRIORITY_BUILDING, 0, 200, -1);
			endif;
		endif;

		//--------------------------------------------------
		// check to see if AI should switch Target
		if (changeTarg AND (getTime > scanTime + 15)) then
			if (curStateHandle <> StartStateHandle) then
				trans start;
			endif;
		endif;	

		//--------------------------------------------------
		// Process the pilot events since the last update...
		numFunctionalWeapons = getWeapons(weaponList, 1);
		if (numFunctionalWeapons == 0) then
			trans noWeapons;
		endif;
		
		//Check if it's time to start the Fireworks
		if (GoCam1) And (Not startAttack) then
			startAttack = True;
			trans StartTheAttack;
		endif;

		processingPilotEvents = TRUE;
		while (processingPilotEvents) do
			pilotEventID = getNextPilotEvent(pilotEventParams);
			if (pilotEventID == PILOT_EVENT_NONE) then
				processingPilotEvents = FALSE;
			else
				switch (pilotEventID)
					case PILOT_EVENT_TARGETED:
						curTarget = getTarget(-1);
						if (lastHelpRequestTime < (curTime - helpRequestFrequency)) then
							lastHelpRequestTime = curTime;
							if (willRequestHelp) then
								//distanceToAttacker = distanceToObject(-1, pilotEventParams[0]);
								getObjectPosition(pilotEventParams[0], attackerPos);
								getObjectPosition(-1, myPos);
								requestHelp(pilotEventParams[0], myPos, attackerHelpRadius, attackerPos, defenderHelpRadius, 1);
							endif;
						endif;
						numFunctionalWeapons = getWeapons(weaponList, 0);
						if (curStateHandle <> AttackStateHandle) then
							if ((numFunctionalWeapons > 0) and (curTarget == 0)) then
								coreAttack(pilotEventParams[0], TACORDER_PARAM_PURSUE);
								setState(AttackStateHandle);
							endif;
						endif;
						endcase;
					case PILOT_EVENT_ATTACK_ORDER:
						curTarget = getTarget(-1);
						if (curStateHandle <> AttackStateHandle) then
							if ((numFunctionalWeapons > 0) and (curTarget == 0))then
								coreAttack(pilotEventParams[0], TACORDER_PARAM_PURSUE);
								setState(AttackStateHandle);
							endif;
						endif;
						endcase;
					case PILOT_EVENT_FIRED_WEAPON:
						endcase;
				endswitch;
			endif;
		endwhile;

		return(0);

endfunction;

//----------------------------------------------------------------------------------------

state noWeapons;

	code

		setDebugString(-1, 3, "   NO WEAPONS ");
		if (objectClass(-1) == 2) then
			coreEject;
		else
			corePower(false);
		endif;
		
endstate;

//----------------------------------------------------------------------------------------

state attack;

	code

		update;
		setDebugString(-1, 3, " ATTACK    ");
		ec1_Group_Run = True;
		ec2_attack_Gulag = True;
		changeTarg = True;
		coreAttack(0, TACORDER_PARAM_NONE);
		resetOrders(1);
		transBack;

endstate;

//----------------------------------------------------------------------------------------

state StartTheAttack;

	code

		update;
		scanTime = getTime;
		changeTarg = False;
		setDebugString(-1, 3, " GUARDStart    ");
		coreGuard(startPosition, -1, attackstateHandle);
		resetOrders(1);

		//************************
		//PLACEHOLDER
		//ec1_Group_Run = TRUE;
endstate;


//----------------------------------------------------------------------------------------

state start;

	code

		update;
		setDebugString(-1, 3, "Waiting   ");

endstate;



//----------------------------------------------------------------------------------------

endfsm.

//****************************************************************************************
