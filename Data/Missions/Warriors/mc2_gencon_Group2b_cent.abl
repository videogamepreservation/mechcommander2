//****************************************************************************************//
// 
//		GROUP 2B
//
//      MISSION:  GENCON
//
//****************************************************************************************//

fsm mc2_gencon_group2b;

	var

		static WorldPosition	startPosition;
		static PatrolState		basePatrolState,group3PatrolState;
		static PatrolPath		basePatrolPath,group3PatrolPath;
		static boolean			willRequestHelp;
		static real				lastHelpRequestTime;
		static real				helpRequestFrequency;
		static real				attackerHelpRadius;
		static real				defenderHelpRadius;
		static integer			AttackStateHandle;
		static integer			GoToBaseStateHandle;
		static integer			Group3AttacksStateHandle;
		static boolean			GoToBaseSet;
		static boolean			Group3AttacksSet;

		static integer			numFunctionalWeapons;
		static integer[20]		weaponList;

//****************************************************************************************

function init;

    code

		//setDebugWindow(-1, -1);

		//---------------------------
		// Grab his start position...
		getObjectPosition(-1, startPosition);

		//-------------------
		// Look for movers...
		setTargetPriority(0, TARGET_PRIORITY_CURTARGET, -1, 400, CONTACT_CRITERIA_ENEMY + CONTACT_CRITERIA_VISUAL_OR_SENSOR + CONTACT_CRITERIA_NOT_DISABLED);
		setTargetPriority(1, TARGET_PRIORITY_MOVER, 0, 150, CONTACT_CRITERIA_ENEMY + CONTACT_CRITERIA_VISUAL_OR_SENSOR + CONTACT_CRITERIA_NOT_DISABLED);
		setTargetPriority(2, TARGET_PRIORITY_NONE, 0, 0, 0);

		AttackStateHandle = getStateHandle("attack");
		GoToBaseStateHandle = getStateHandle("goToBase");
		Group3AttacksStateHandle = getStateHandle("group3Attacks");

		GoToBaseSet = false;
		Group3AttacksSet = false;

		basePatrolState[0] = PATROL_TYPE_LINEAR;    
		basePatrolState[1] = 2;    //?num points
		basePatrolState[2] = 1;    //?num cycles
		basePatrolState[3] = PATROL_DIRECTION_FORWARD;    
		basePatrolState[4] = -1;    //reset cur point
		basePatrolState[5] = -1;    //reset cur cycle
		basePatrolState[6] = CONTACT_CRITERIA_ENEMY + CONTACT_CRITERIA_VISUAL_OR_SENSOR + CONTACT_CRITERIA_NOT_DISABLED;    
		basePatrolPath[0, 0] = 64.0;
		basePatrolPath[0, 1] = 2752.0;
		basePatrolPath[1, 0] = -1770.667;
		basePatrolPath[1, 1] = 2752.0;

		group3PatrolState[0] = PATROL_TYPE_LINEAR;    
		group3PatrolState[1] = 2;    //?num points
		group3PatrolState[2] = 1;    //?num cycles
		group3PatrolState[3] = PATROL_DIRECTION_FORWARD;    
		group3PatrolState[4] = -1;    //reset cur point
		group3PatrolState[5] = -1;    //reset cur cycle
		group3PatrolState[6] = CONTACT_CRITERIA_ENEMY + CONTACT_CRITERIA_VISUAL + CONTACT_CRITERIA_NOT_DISABLED;    
		group3PatrolPath[0, 0] = 3349.333;
		group3PatrolPath[0, 1] = -1557.333;
		group3PatrolPath[1, 0] = 4074.666;
		group3PatrolPath[1, 1] = 21.333;

		willRequestHelp = true; //?true or false
		helpRequestFrequency = 20.0; //?in secs
		attackerHelpRadius = 100; //?in meters
		defenderHelpRadius = 225; //?in meters
		lastHelpRequestTime = -100.0;
						
endfunction;


//----------------------------------------------------------------------------------------

function update : integer;

	var

		boolean		processingPilotEvents;
		boolean		thinking;
		integer		pilotEventID;
		integer		pilotState;
		integer[20]	pilotEventParams;
		integer		curTarget;
		real		curTime;
		real[3]		myPos;
		real[3]		attackerPos;
		real		distanceToAttacker;
		integer		curStateHandle;


	code

		curTime = getTime;

		//-----------------------------------
		// Is it time to move to a new state?
		curStateHandle = getCurrentStateHandle;
		if (GoToBaseFlag) then
			if (not GoToBaseSet) then
				GoToBaseSet = true;
				if (curStateHandle <> GoToBaseStateHandle) then
					mcprint("trans GoToBase");
					trans GoToBase;
				endif;
			endif;
		else
			if (Group3AttacksFlag) then
				if (not Group3AttacksSet) then
					Group3AttacksSet = true;
					if (curStateHandle <> Group3AttacksStateHandle) then
						mcprint("trans Group3Attacks");
						trans Group3Attacks;
					endif;
				endif;
			endif;
		endif;

		//--------------------------------------------------
		// Process the pilot events since the last update...
		numFunctionalWeapons = getWeapons(weaponList, 1);
		if (numFunctionalWeapons == 0) then
			trans noWeapons;
		endif;

		processingPilotEvents = TRUE;
		while (processingPilotEvents) do
			pilotEventID = getNextPilotEvent(pilotEventParams);
			if (pilotEventID == PILOT_EVENT_NONE) then
				processingPilotEvents = FALSE;
			else
				switch (pilotEventID)
					case PILOT_EVENT_TARGETED:
						curTarget = getTarget(-1);
						if (lastHelpRequestTime < (curTime - helpRequestFrequency)) then
							lastHelpRequestTime = curTime;
							if (willRequestHelp) then
								//distanceToAttacker = distanceToObject(-1, pilotEventParams[0]);
								getObjectPosition(pilotEventParams[0], attackerPos);
								getObjectPosition(-1, myPos);
								requestHelp(pilotEventParams[0], myPos, attackerHelpRadius, attackerPos, defenderHelpRadius, 1);
							endif;
						endif;
						numFunctionalWeapons = getWeapons(weaponList, 0);
						if (curStateHandle <> AttackStateHandle) then
							if ((numFunctionalWeapons > 0) and (curTarget == 0)) then
								coreAttack(pilotEventParams[0], TACORDER_PARAM_PURSUE);
								setState(AttackStateHandle);
							endif;
						endif;
						endcase;
					case PILOT_EVENT_ATTACK_ORDER:
						curTarget = getTarget(-1);
						if (curStateHandle <> AttackStateHandle) then
							if ((numFunctionalWeapons > 0) and (curTarget == 0))then
								coreAttack(pilotEventParams[0], TACORDER_PARAM_PURSUE);
								setState(AttackStateHandle);
							endif;
						endif;
						endcase;
					case PILOT_EVENT_FIRED_WEAPON:
						endcase;
				endswitch;
			endif;
		endwhile;

		return(0);

endfunction;

//----------------------------------------------------------------------------------------

state noWeapons;

	code

		setDebugString(-1, 3, "   NO WEAPONS ");
		if (objectClass(-1) == 2) then
			coreEject;
		else
			corePower(false);
		endif;
		
endstate;

//----------------------------------------------------------------------------------------


state attack;

	var

		integer		tacticState;

	code

		update;
		setDebugString(-1, 3, " ATTACK    ");
		Group2AttacksFlag = true;
		coreAttackTactic(0, TACORDER_PARAM_RUN, TACTIC_REAR, tacticState);
//		coreAttack(0, TACORDER_PARAM_PURSUE);
		resetOrders(1);
		transBack;

endstate;

//----------------------------------------------------------------------------------------

state goToBase;

	var

		WorldPosition	baseLocation;

	code

		update;
		setDebugString(-1, 3, "   gotToBase    ");

		baseLocation[0] = -1770.667;
		baseLocation[1] = 2752.0;
		baseLocation[2] = 0.0;
		if (numFunctionalWeapons > 0) then
			corePatrol(basePatrolState, basePatrolPath, AttackStateHandle);
			coreGuard(baseLocation, -1, AttackStateHandle);
		else
			setDebugString(-1, 3, " gotToBase(NO WP) ");
			setMoveArea(startPosition, -1.0);
			corePatrol(basePatrolState, basePatrolPath, -1);
			trans noWeapons;
		endif;

endstate;

//----------------------------------------------------------------------------------------

state group3Attacks;

	var

		WorldPosition	group3Loc;

	code

		update;
		setMoveArea(startPosition, -1.0);
		setDebugString(-1, 3, "group3Attacks");
		if (numFunctionalWeapons > 0) then
			CoreRun = true;
			corePatrol(group3PatrolState, group3PatrolPath, AttackStateHandle);
			group3Loc[0] = 4074.666;
			group3Loc[1] = 21.333;
			group3Loc[2] = 0.0;
			coreGuard(group3Loc, -1, AttackStateHandle);
		else
			trans noWeapons;
		endif;

endstate;

//----------------------------------------------------------------------------------------

state start;

	code

		update;
		setDebugString(-1, 3, "   GUARD    ");
		setMoveArea(startPosition, -1.0);
		numFunctionalWeapons = getWeapons(weaponList, 1);
		if (numFunctionalWeapons > 0) then
			coreGuard(startPosition, -1, AttackStateHandle);
		else
			coreMoveTo(startPosition, 0);
			resetOrders(1);
		endif;

endstate;

//----------------------------------------------------------------------------------------

endfsm.

//****************************************************************************************
