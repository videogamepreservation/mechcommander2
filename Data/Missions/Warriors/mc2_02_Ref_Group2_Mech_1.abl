//****************************************************************************************//
// 
//		This Brain is used for Reinforcement Group 2
//			FireAnts
//
//      MISSION:  Mc2_2 Tutorial Brain
//
//
//****************************************************************************************//

fsm	mc2_02_Ref_Group2_Mech_1;

	var
		static WorldPosition		startPosition;
		static PatrolState		startPatrolState, BasePatrolState, WaitPatrolState;
		static PatrolPath			startPatrolPath, BasePatrolPath, WaitPatrolPath;
		static boolean			willRequestHelp;
		static real				lastHelpRequestTime;
		static real				helpRequestFrequency;
		static real				attackerHelpRadius;
		static real				defenderHelpRadius;
		static integer			AttackStateHandle;

		static boolean			poweredDown;


		static integer			numFunctionalWeapons;
		static integer[20]		weaponList;

		static boolean			send_ReinForce_Check;
		static boolean			wait4Vehicles_Check;


//****************************************************************************************

function init;

    code

		//setDebugWindow(-1, -1);

		//---------------------------
		// Grab his start position...
		getObjectPosition(-1, startPosition);

		//-------------------
		// Look for movers...
		setTargetPriority(0, TARGET_PRIORITY_CURTARGET, -1, 150, CONTACT_CRITERIA_ENEMY + CONTACT_CRITERIA_VISUAL_OR_SENSOR + CONTACT_CRITERIA_NOT_DISABLED);
		setTargetPriority(1, TARGET_PRIORITY_MOVER, 0, 300, CONTACT_CRITERIA_ENEMY + CONTACT_CRITERIA_VISUAL_OR_SENSOR + CONTACT_CRITERIA_NOT_DISABLED);

		AttackStateHandle = getStateHandle("attack");
		send_ReinForce_Check = False;
		wait4Vehicles_Check = False;

		//=================================
		//Path Used for Entering the Base
		startPatrolState[0] = PATROL_TYPE_LINEAR;    
		startPatrolState[1] = 2;    //?num points
		startPatrolState[2] = 1;    //?num cycles
		startPatrolState[3] = PATROL_DIRECTION_FORWARD;    
		startPatrolState[4] = -1;    //reset cur point
		startPatrolState[5] = -1;    //reset cur cycle
		startPatrolState[6] = CONTACT_CRITERIA_ENEMY + CONTACT_CRITERIA_VISUAL_OR_SENSOR + CONTACT_CRITERIA_NOT_DISABLED;    
		startPatrolPath[0, 0] = -2154;
		startPatrolPath[0, 1] = -1557;		
		startPatrolPath[1, 0] = -576;
		startPatrolPath[1, 1] = -1173;

		//=================================
		//Path Used for Patrol Base after Assault
		BasePatrolState[0] = PATROL_TYPE_LOOPING;    
		BasePatrolState[1] = 2;    //?num points
		BasePatrolState[2] = -1;    //?num cycles
		BasePatrolState[3] = PATROL_DIRECTION_FORWARD;    
		BasePatrolState[4] = -1;    //reset cur point
		BasePatrolState[5] = -1;    //reset cur cycle
		BasePatrolState[6] = CONTACT_CRITERIA_ENEMY + CONTACT_CRITERIA_VISUAL_OR_SENSOR + CONTACT_CRITERIA_NOT_DISABLED;    
		BasePatrolPath[0, 0] = 234;
		BasePatrolPath[0, 1] = -2368;
		BasePatrolPath[1, 0] = -874;
		BasePatrolPath[1, 1] = -2282;

		//=================================
		//Path used for Waiting for Vehicles to Join UP
		WaitPatrolState[0] = PATROL_TYPE_LOOPING;    
		WaitPatrolState[1] = 2;    //?num points
		WaitPatrolState[2] = 1;    //?num cycles
		WaitPatrolState[3] = PATROL_DIRECTION_FORWARD;    
		WaitPatrolState[4] = -1;    //reset cur point
		WaitPatrolState[5] = -1;    //reset cur cycle
		WaitPatrolState[6] = CONTACT_CRITERIA_ENEMY + CONTACT_CRITERIA_VISUAL_OR_SENSOR + CONTACT_CRITERIA_NOT_DISABLED;    
		WaitPatrolPath[0, 0] = -5312;
		WaitPatrolPath[0, 1] = -1258;
		WaitPatrolPath[1, 0] = -3818;
		WaitPatrolPath[1, 1] = -1318;


		willRequestHelp = true; //?true or false
		helpRequestFrequency = 20.0; //?in secs
		attackerHelpRadius = 100; //?in meters
		defenderHelpRadius = 125; //?in meters
		lastHelpRequestTime = -100.0;
						
endfunction;


//----------------------------------------------------------------------------------------

function setWillRequestHelp (boolean setting);

	code

		if (setting and (not willRequestHelp)) then
			lastHelpRequestTime = 0.0;
		endif;
		willRequestHelp = setting;

endfunction;

//----------------------------------------------------------------------------------------

function update : integer;

	var

		boolean		processingPilotEvents;
		boolean		thinking;
		integer		pilotEventID;
		integer		pilotState;
		integer[20]		pilotEventParams;
		integer		curTarget;
		real			curTime;
		real[3]		myPos;
		real[3]		attackerPos;
		real			distanceToAttacker;
		integer		curStateHandle;


	code

		curTime = getTime;
		curStateHandle = getCurrentStateHandle;


		//--------------------------------------------------
		// Process the pilot events since the last update...
		numFunctionalWeapons = getWeapons(weaponList, 1);
		if (numFunctionalWeapons == 0) then
			trans noWeapons;
		endif;

		//--------------------------------------------------
		// Move Wait Point
		if (Call_Reinf_Group2) And (Not send_ReinForce_Check) then
			send_ReinForce_Check = True;
			trans attackBase;
		endif;

		//--------------------------------------------------
		// Attack Base
		if (group2_AttackBase) And (Not wait4Vehicles_Check) then
			wait4Vehicles_Check = True;
			trans MoveToBase;
		endif;



		processingPilotEvents = TRUE;
		while (processingPilotEvents) do
			pilotEventID = getNextPilotEvent(pilotEventParams);
			if (pilotEventID == PILOT_EVENT_NONE) then
				processingPilotEvents = FALSE;
			else
				switch (pilotEventID)
					case PILOT_EVENT_TARGETED:
						curTarget = getTarget(-1);
						if (lastHelpRequestTime < (curTime - helpRequestFrequency)) then
							lastHelpRequestTime = curTime;
							if (willRequestHelp) then
								//distanceToAttacker = distanceToObject(-1, pilotEventParams[0]);
								getObjectPosition(pilotEventParams[0], attackerPos);
								getObjectPosition(-1, myPos);
								requestHelp(pilotEventParams[0], myPos, attackerHelpRadius, attackerPos, defenderHelpRadius, 1);
							endif;
						endif;
						numFunctionalWeapons = getWeapons(weaponList, 0);
						if (curStateHandle <> AttackStateHandle) then
							if ((numFunctionalWeapons > 0) and (curTarget == 0)) then
								coreAttack(pilotEventParams[0], TACORDER_PARAM_PURSUE);
								setState(AttackStateHandle);
							endif;
						endif;
						endcase;
					case PILOT_EVENT_ATTACK_ORDER:
						curTarget = getTarget(-1);
						if (curStateHandle <> AttackStateHandle) then
							if ((numFunctionalWeapons > 0) and (curTarget == 0))then
								coreAttack(pilotEventParams[0], TACORDER_PARAM_PURSUE);
								setState(AttackStateHandle);
							endif;
						endif;
						endcase;
					case PILOT_EVENT_FIRED_WEAPON:
						endcase;
				endswitch;
			endif;
		endwhile;

		return(0);

endfunction;

//----------------------------------------------------------------------------------------

state noWeapons;

	code

		setDebugString(-1, 3, "   NO WEAPONS ");
		if (objectClass(-1) == 2) then
			coreEject;
		else
			corePower(false);
		endif;
		
endstate;

//----------------------------------------------------------------------------------------

state attack;

	var
		integer		tacticState;

	code

		update;
		setDebugString(-1, 3, " ATTACK    ");
		//coreAttackTactic(0, TACORDER_PARAM_RUN, TACTIC_REAR, tacticState);
		coreAttack(0, TACORDER_PARAM_PURSUE);
		resetOrders(1);
		transBack;

endstate;
//----------------------------------------------------------------------------------------

state attackBase;

	code
		

		update;
		setDebugString(-1, 3, "WaitPath    ");
		corePatrol(WaitPatrolState, WaitPatrolPath, attackstateHandle);
		wait4Vehicles = True;

endstate;

//----------------------------------------------------------------------------------------
state moveToBase;

	code
		

		update;
		setDebugString(-1, 3, "BaseAssault    ");
		corePatrol(startPatrolState, startPatrolPath, attackstateHandle);
		trans patrolBase;


endstate;
//---------------------------------------------------------------------------------------

state patrolBase;


	code
		
		update;
		setDebugString(-1, 3, "GuardBase    ");
		corePatrol(BasePatrolState, BasePatrolPath, attackstateHandle);
		resetOrders(1);

endstate;
//----------------------------------------------------------------------------------------

state start;

	code


		update;
		setDebugString(-1, 3, "   GUARD    ");
		coreGuard(startPosition, -1, attackstateHandle);
		//corePatrol(startPatrolState, startPatrolPath, attackstateHandle);
		resetOrders(1);
		 
endstate;

//----------------------------------------------------------------------------------------

endfsm.

//****************************************************************************************
