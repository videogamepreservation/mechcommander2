//****************************************************************************************//
//
//		Brain used for the 1st Wave of Liao Reinfor
//			CenterGroup
//
//      MISSION:  Mc2_17
//
//
//****************************************************************************************//

fsm mc2_17_a2_Liao_Mech_1a;

	var

		static WorldPosition		startPosition;
		static PatrolState		startPatrolState,  BasePatrolState;
		static PatrolPath			startPatrolPath,  BasePatrolPath;
		static boolean			willRequestHelp;
		static real				lastHelpRequestTime;
		static real				helpRequestFrequency;
		static real				attackerHelpRadius;
		static real				defenderHelpRadius;
		static integer			AttackStateHandle;

		static boolean			poweredDown;

		static integer			numFunctionalWeapons;
		static integer[20]		weaponList;
		static boolean			goReinforcements;

		static boolean			goGuardBase;

		static boolean			goPatroling;
		static Real				goPatroling_Timer;

//****************************************************************************************

function init;

    code

		//setDebugWindow(-1, -1);

		//---------------------------
		// Grab his start position...
		getObjectPosition(-1, startPosition);

		//-------------------
		// Look for movers...
		setTargetPriority(0, TARGET_PRIORITY_CURTARGET, -1, 300, CONTACT_CRITERIA_ENEMY + CONTACT_CRITERIA_VISUAL_OR_SENSOR + CONTACT_CRITERIA_NOT_DISABLED);
		setTargetPriority(1, TARGET_PRIORITY_MOVER, 0, 350, CONTACT_CRITERIA_ENEMY + CONTACT_CRITERIA_VISUAL_OR_SENSOR + CONTACT_CRITERIA_NOT_DISABLED);

		AttackStateHandle = getStateHandle("attack");
		goReinforcements = False;
		goGuardBase = False;
		goPatroling = False;
		goPatroling_Timer = 0;

		startPatrolState[0] = PATROL_TYPE_LINEAR;
		startPatrolState[1] = 3;    //?num points
		startPatrolState[2] = 1;    //?num cycles
		startPatrolState[3] = PATROL_DIRECTION_FORWARD;
		startPatrolState[4] = -1;    //reset cur point
		startPatrolState[5] = -1;    //reset cur cycle
		startPatrolState[6] = CONTACT_CRITERIA_ENEMY + CONTACT_CRITERIA_VISUAL_OR_SENSOR + CONTACT_CRITERIA_NOT_DISABLED;
		startPatrolPath[0, 0] = 1002.667 + 88;
		startPatrolPath[0, 1] = -7018.666;
		startPatrolPath[1, 0] = 1728 + 88;
		startPatrolPath[1, 1] = -4160;
		startPatrolPath[2, 0] = 1728 + 44;
		startPatrolPath[2, 1] = -4072 - 44;
		
		BasePatrolState[0] = PATROL_TYPE_LINEAR;
		BasePatrolState[1] = 3;    //?num points
		BasePatrolState[2] = -1;    //?num cycles
		BasePatrolState[3] = PATROL_DIRECTION_FORWARD;
		BasePatrolState[4] = -1;    //reset cur point
		BasePatrolState[5] = -1;    //reset cur cycle
		BasePatrolState[6] = CONTACT_CRITERIA_ENEMY + CONTACT_CRITERIA_VISUAL_OR_SENSOR + CONTACT_CRITERIA_NOT_DISABLED;
		BasePatrolPath[0, 0] = 3093;
		BasePatrolPath[0, 1] = -661;
		BasePatrolPath[1, 0] = 4586;
		BasePatrolPath[1, 1] = 1472;
		BasePatrolPath[2, 0] = 5525;
		BasePatrolPath[2, 1] = 3690;


		willRequestHelp = true; //?true or false
		helpRequestFrequency = 20.0; //?in secs
		attackerHelpRadius = 100; //?in meters
		defenderHelpRadius = 125; //?in meters
		lastHelpRequestTime = -100.0;

		setWillHelp(True);
						
endfunction;


//----------------------------------------------------------------------------------------

function setWillRequestHelp (boolean setting);

	code

		if (setting and (not willRequestHelp)) then
			lastHelpRequestTime = 0.0;
		endif;
		willRequestHelp = setting;

endfunction;

//----------------------------------------------------------------------------------------

function update : integer;

	var

		boolean		processingPilotEvents;
		boolean		thinking;
		integer		pilotEventID;
		integer		pilotState;
		integer[20]		pilotEventParams;
		integer		curTarget;
		real			curTime;
		real[3]		myPos;
		real[3]		attackerPos;
		real			distanceToAttacker;
		integer		curStateHandle;


	code

		curTime = getTime;
		curStateHandle = getCurrentStateHandle;

		LiaoReinForce_Cat_2 = getID;


		//--------------------------------------------------
		// Process the pilot events since the last update...
		numFunctionalWeapons = getWeapons(weaponList, 1);
		if (numFunctionalWeapons == 0) then
			trans noWeapons;
		endif;

		//--------------------------------------------------
		// Check to see if time to attack the player
		if (LiaoReinforce_1_Center) and (Not goGuardBase) then
			goGuardBase = True;
			goPatroling_Timer = getTime + LiaoReinforce_Wait_Timer;
			trans GuardBase;
		endif;


		//--------------------------------------------------
		// Check to see if time to guard base
		if (LiaoSendReinforce) and (Not goReinforcements) then
			goReinforcements = True;
			trans goReinforce;
		endif;


		//--------------------------------------------------			
		// Check to see if time to Patrol the Rebel Area
		if ((getTime > goPatroling_Timer) and (Not goPatroling) and (goPatroling_Timer > 0)) then
			goPatroling = True;
			trans PatrolBase;
		endif;

		processingPilotEvents = TRUE;
		while (processingPilotEvents) do
			pilotEventID = getNextPilotEvent(pilotEventParams);
			if (pilotEventID == PILOT_EVENT_NONE) then
				processingPilotEvents = FALSE;
			else
				switch (pilotEventID)
					case PILOT_EVENT_TARGETED:
						curTarget = getTarget(-1);
						if (lastHelpRequestTime < (curTime - helpRequestFrequency)) then
							lastHelpRequestTime = curTime;
							if (willRequestHelp) then
								//distanceToAttacker = distanceToObject(-1, pilotEventParams[0]);
								getObjectPosition(pilotEventParams[0], attackerPos);
								getObjectPosition(-1, myPos);
								requestHelp(pilotEventParams[0], myPos, attackerHelpRadius, attackerPos, defenderHelpRadius, 1);
							endif;
						endif;
						numFunctionalWeapons = getWeapons(weaponList, 0);
						if (curStateHandle <> AttackStateHandle) then
							if ((numFunctionalWeapons > 0) and (curTarget == 0)) then
								coreAttack(pilotEventParams[0], TACORDER_PARAM_PURSUE);
								setState(AttackStateHandle);
							endif;
						endif;
						endcase;
					case PILOT_EVENT_ATTACK_ORDER:
						curTarget = getTarget(-1);
						if (curStateHandle <> AttackStateHandle) then
							if ((numFunctionalWeapons > 0) and (curTarget == 0))then
								coreAttack(pilotEventParams[0], TACORDER_PARAM_PURSUE);
								setState(AttackStateHandle);
							endif;
						endif;
						endcase;
					case PILOT_EVENT_FIRED_WEAPON:
						endcase;
				endswitch;
			endif;
		endwhile;

		return(0);

endfunction;

//----------------------------------------------------------------------------------------

state noWeapons;

	code

		setDebugString(-1, 3, "   NO WEAPONS ");
		if (objectClass(-1) == 2) then
			coreEject;
		else
			corePower(false);
		endif;
		
endstate;

//----------------------------------------------------------------------------------------

state attack;

	var
		integer		tacticState;

	code

		update;
		setDebugString(-1, 3, " ATTACK    ");
		AttackMusicTrigger1 = TRUE;
		//coreAttackTactic(0, TACORDER_PARAM_NONE, TACTIC_JOUST, tacticState);
		coreAttack(0, TACORDER_PARAM_PURSUE);
		resetOrders(1);
		transBack;

endstate;

//----------------------------------------------------------------------------------------

state goReinforce;

	code

		update;

		setDebugString(-1, 3, "goReinforce ");	
		corePatrol(startPatrolState, startPatrolPath, AttackStateHandle);
		resetOrders(1);
		trans Wait4Force;
	
endstate;

//----------------------------------------------------------------------------------------

state Wait4Force;

	var
		worldPosition		GuardSpot;

	code

		guardSpot[0] = startPatrolPath[2, 0];
		guardSpot[1] = startPatrolPath[2, 1];
		guardSpot[2] = 0;

		update;
		setDebugString(-1, 3, "Wait4Force ");
		coreGuard(GuardSpot, -1, AttackStateHandle);
		resetOrders(1);
	
endstate;

//----------------------------------------------------------------------------------------

state GuardBase;

	var
		worldPosition		GuardSpot;

	code

		guardSpot[0] = 2069;
		guardSpot[1] = -1216;
		guardSpot[2] = 0;
		
		update;
		setDebugString(-1, 3, "GuardBase  ");
		coreGuard(GuardSpot, -1, AttackStateHandle);
		resetOrders(1);

	
endstate;


//----------------------------------------------------------------------------------------

state PatrolBase;

	code

		update;
		setDebugString(-1, 3, "PatrolBase ");	
		corePatrol(basePatrolState, basePatrolPath, AttackStateHandle);
		resetOrders(1);

	
endstate;

//----------------------------------------------------------------------------------------

state start;

	code

		update;

endstate;

//----------------------------------------------------------------------------------------

endfsm.

//****************************************************************************************
