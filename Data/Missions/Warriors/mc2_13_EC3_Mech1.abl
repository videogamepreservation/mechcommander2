//****************************************************************************************//
// 
//		Ecounter 3 Mech patroling at the Repair Bay
//			WolfHound
//
//      MISSION:  mc2_13 
//		This unit will scan for the player. 
//		AI will Run Away after 10 sec in Combat
//		And attack the player if detected
//
//****************************************************************************************//

fsm mc2_13_EC3_Mech1;

	var

		static WorldPosition		startPosition;
		static PatrolState		startPatrolState, RunAwayPatrolState;
		static PatrolPath			startPatrolPath, RunAwayPatrolPath;
		static boolean			willRequestHelp;
		static real				lastHelpRequestTime;
		static real				helpRequestFrequency;
		static real				attackerHelpRadius;
		static real				defenderHelpRadius;
		static integer			AttackStateHandle, RunAwayStateHandle;

		static integer			numFunctionalWeapons;
		static integer[20]		weaponList;

		static boolean			runAwayTimerCheck, runAwayTrans;
		
		static real				runAwayTime;

//****************************************************************************************

function init;

    code

		//setDebugWindow(-1, -1);

		//---------------------------
		// Grab his start position...
		getObjectPosition(-1, startPosition);

		//-------------------
		// Look for movers...
		setTargetPriority(0, TARGET_PRIORITY_CURTARGET, -1, 150, CONTACT_CRITERIA_ENEMY + CONTACT_CRITERIA_VISUAL_OR_SENSOR + CONTACT_CRITERIA_NOT_DISABLED);
		setTargetPriority(1, TARGET_PRIORITY_MOVER, 0, 400, CONTACT_CRITERIA_ENEMY + CONTACT_CRITERIA_VISUAL_OR_SENSOR + CONTACT_CRITERIA_NOT_DISABLED);
		setTargetPriority(2, TARGET_PRIORITY_NONE, 0, 0, 0);

		AttackStateHandle = getStateHandle("attack");
		RunAwayStateHandle = getStateHandle("RunAway");
		runAwayTimerCheck = False;
		runAwayTrans = False;

		startPatrolState[0] = PATROL_TYPE_LINEAR;    
		startPatrolState[1] = 3;    //?num points
		startPatrolState[2] = -1;    //?num cycles
		startPatrolState[3] = PATROL_DIRECTION_FORWARD;    
		startPatrolState[4] = -1;    //reset cur point
		startPatrolState[5] = -1;    //reset cur cycle
		startPatrolState[6] = CONTACT_CRITERIA_ENEMY + CONTACT_CRITERIA_VISUAL_OR_SENSOR + CONTACT_CRITERIA_NOT_DISABLED;    
		startPatrolPath[0, 0] = -3818.667;
		startPatrolPath[0, 1] = 5525.333;
		startPatrolPath[1, 0] = -3562.667;
		startPatrolPath[1, 1] = 4842.667;
		startPatrolPath[2, 0] = -3392.667;
		startPatrolPath[2, 1] = 3733.333;

		RunAwayPatrolState[0] = PATROL_TYPE_LINEAR;    
		RunAwayPatrolState[1] = 2;    //?num points
		RunAwayPatrolState[2] = 1;    //?num cycles
		RunAwayPatrolState[3] = PATROL_DIRECTION_FORWARD;    
		RunAwayPatrolState[4] = -1;    //reset cur point
		RunAwayPatrolState[5] = -1;    //reset cur cycle
		RunAwayPatrolState[6] = CONTACT_CRITERIA_ENEMY + CONTACT_CRITERIA_VISUAL_OR_SENSOR + CONTACT_CRITERIA_NOT_DISABLED;    
		RunAwayPatrolPath[0, 0] = -3818.667;
		RunAwayPatrolPath[0, 1] = 5525.333;
		RunAwayPatrolPath[1, 0] = -4800.000;
		RunAwayPatrolPath[1, 1] = 4928.000;


		willRequestHelp = False; //?true or false
		helpRequestFrequency = 10.0; //?in secs
		attackerHelpRadius = 50; //?in meters
		defenderHelpRadius = 50; //?in meters
		lastHelpRequestTime = -100.0;
						
endfunction;


//----------------------------------------------------------------------------------------

function setWillRequestHelp (boolean setting);

	code

		if (setting and (not willRequestHelp)) then
			lastHelpRequestTime = 0.0;
		endif;
		willRequestHelp = setting;

endfunction;

//----------------------------------------------------------------------------------------

function update : integer;

	var

		boolean		processingPilotEvents;
		boolean		thinking;
		integer		pilotEventID;
		integer		pilotState;
		integer[20]		pilotEventParams;
		integer		curTarget;
		real			curTime;
		real[3]		myPos;
		real[3]		attackerPos;
		real			distanceToAttacker;
		integer		curStateHandle;
		



	code
		//--------------------------------------------------
		// Check Time and Check Current State Handle
		curTime = getTime;
		curStateHandle = getCurrentStateHandle;
		//--------------------------------------------------

		//--------------------------------------------------
		//Sets Timer to RunAwayTime
		if (RepairPatrolBeenAttack) and (Not runAwayTimerCheck) Then
			runAwayTimerCheck = True;
			runAwayTime = getTime + 10;
		endif;
		
		//--------------------------------------------------
		//Sets Timer to RunAwayTime
		if (getTime > runAwayTime) AND (Not runAwayTrans) AND (runAwayTimerCheck) Then
			runAwayTrans = True;
			trans RunAway;
		endif;


		//--------------------------------------------------
		// Process the pilot events since the last update...
/*		numFunctionalWeapons = getWeapons(weaponList, 1);
		if (numFunctionalWeapons == 0) then
			trans noWeapons;
		endif;
*/
		processingPilotEvents = TRUE;
		while (processingPilotEvents) do
			pilotEventID = getNextPilotEvent(pilotEventParams);
			if (pilotEventID == PILOT_EVENT_NONE) then
				processingPilotEvents = FALSE;
			else
				switch (pilotEventID)
					case PILOT_EVENT_TARGETED:
						curTarget = getTarget(-1);
						if (lastHelpRequestTime < (curTime - helpRequestFrequency)) then
							lastHelpRequestTime = curTime;
							if (willRequestHelp) then
								//distanceToAttacker = distanceToObject(-1, pilotEventParams[0]);
								getObjectPosition(pilotEventParams[0], attackerPos);
								getObjectPosition(-1, myPos);
								requestHelp(pilotEventParams[0], myPos, attackerHelpRadius, attackerPos, defenderHelpRadius, 1);
							endif;
						endif;
						numFunctionalWeapons = getWeapons(weaponList, 0);
						if (curStateHandle <> AttackStateHandle) then
							if ((numFunctionalWeapons > 0) and (curTarget == 0)) then
								coreAttack(pilotEventParams[0], TACORDER_PARAM_PURSUE);
								setState(AttackStateHandle);
							endif;
						endif;
						endcase;
					case PILOT_EVENT_ATTACK_ORDER:
						curTarget = getTarget(-1);
						if (curStateHandle <> AttackStateHandle) then
							if ((numFunctionalWeapons > 0) and (curTarget == 0))then
								coreAttack(pilotEventParams[0], TACORDER_PARAM_PURSUE);
								setState(AttackStateHandle);
							endif;
						endif;
						endcase;
					case PILOT_EVENT_FIRED_WEAPON:
						endcase;
				endswitch;
			endif;
		endwhile;

		return(0);

endfunction;

//----------------------------------------------------------------------------------------

state noWeapons;

	code

		setDebugString(-1, 3, "   NO WEAPONS ");
		if (objectClass(-1) == 2) then
			coreEject;
		else
			corePower(false);
		endif;
		
endstate;

//----------------------------------------------------------------------------------------

state RunAway;

	code

		update;
		setDebugString(-1, 3, " RUNAWAY    ");
		//coreRun = True;
		corePatrol(RunAwayPatrolState, RunAwayPatrolPath, -1);
		trans GuardRepairBay;

endstate;

//----------------------------------------------------------------------------------------
state GuardRepairBay;

	var
		worldPosition 		GuardRepairBay;


	code
		GuardRepairBay[0] = -5056.000;
		GuardRepairBay[1] = 4501.000;
		GuardRepairBay[2] = 0.0;

		update;
		setDebugString(-1, 3, " Guard_RBay    ");
		coreGuard(GuardRepairBay, -1 , AttackStateHandle);
		resetOrders(1);
		transBack;

endstate;

//----------------------------------------------------------------------------------------

state attack;
	var

		integer		tacticState;
	code

		RepairPatrolBeenAttack  = True;

		update;
		setDebugString(-1, 3, " ATTACK    ");
//		coreAttackTactic(0, TACORDER_PARAM_NONE, TACTIC_RIGHT_FLANK, tacticState);
		coreAttack(0, TACORDER_PARAM_PURSUE);
		resetOrders(1);
		transBack;

endstate;

//----------------------------------------------------------------------------------------

state start;

	code

		update;
		setDebugString(-1, 3, "StartPatrol1     ");
		//coreGuard(startPosition, -1 , AttackStateHandle);
		corePatrol(startPatrolState, startPatrolPath, AttackStateHandle);
		resetOrders(1);
endstate;

//----------------------------------------------------------------------------------------

endfsm.

//****************************************************************************************
