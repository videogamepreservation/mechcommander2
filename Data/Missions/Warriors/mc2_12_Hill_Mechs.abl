	//****************************************************************************************//
//
//		Hill - Mech
//			Group of ??? Mechs
//
//      MISSION:  Mc2_12
//		Unit(s) that GUARD HILL and if TRIGGERED, enter CITY
//		
//
//****************************************************************************************//

fsm mc2_12_Hill_Mechs;

	var

		static WorldPosition		startPosition;
		static PatrolState		startPatrolState;
		static PatrolPath			startPatrolPath;
		static boolean			willRequestHelp;
		static real				lastHelpRequestTime;
		static real				helpRequestFrequency;
		static real				attackerHelpRadius;
		static real				defenderHelpRadius;
		static integer			AttackStateHandle;

		static integer			numFunctionalWeapons;
		static integer[20]		weaponList;

//****************************NEW*********************
		static boolean			poweredDown;

		static PatrolState		startCityPatrolState;
		static PatrolPath		startCityPatrolPath;
		static boolean			CityAttackTest;

		static boolean			Hill_Area_Trigger_Check;


//****************************************************************************************

function init;

    code

//**************ORIGINAL******************

		//setDebugWindow(-1, -1);

		//---------------------------
		// Grab his start position...
		getObjectPosition(-1, startPosition);

		//-------------------
		// Look for movers...
		setTargetPriority(0, TARGET_PRIORITY_CURTARGET, -1, 150, CONTACT_CRITERIA_ENEMY + CONTACT_CRITERIA_VISUAL_OR_SENSOR + CONTACT_CRITERIA_NOT_DISABLED);
		setTargetPriority(1, TARGET_PRIORITY_MOVER, 0, 400, CONTACT_CRITERIA_ENEMY + CONTACT_CRITERIA_VISUAL_OR_SENSOR + CONTACT_CRITERIA_NOT_DISABLED);
		setTargetPriority(2, TARGET_PRIORITY_NONE, 0, 0, 0);

		AttackStateHandle = getStateHandle("attack");

//**************NEW***********************


		//-------------------------------------------
		// Setup the Patrol here...
		//XL startCityPatrolState Start

		startCityPatrolState[0] = PATROL_TYPE_LINEAR;
		startCityPatrolState[1] = 5;    //?num points
		startCityPatrolState[2] = -1;    //?num cycles
		startCityPatrolState[3] = PATROL_DIRECTION_FORWARD;
		startCityPatrolState[4] = -1;    //reset cur point
		startCityPatrolState[5] = -1;    //reset cur cycle
		startCityPatrolState[6] = 0 + CONTACT_CRITERIA_ENEMY + CONTACT_CRITERIA_VISUAL_OR_SENSOR + CONTACT_CRITERIA_NOT_DISABLED;

		startCityPatrolPath[0, 0] = 1088.000;
		startCityPatrolPath[0, 1] = -320.000;
		startCityPatrolPath[1, 0] = 1088.000;
		startCityPatrolPath[1, 1] = -1600.000;
		startCityPatrolPath[2, 0] = -320.000;
		startCityPatrolPath[2, 1] = 1728.000;
		startCityPatrolPath[3, 0] = -320.000;
		startCityPatrolPath[3, 1] = 960.000;
		startCityPatrolPath[4, 0] = 832.000;
		startCityPatrolPath[4, 1] = 2112.000;
		
		
		//XLs startCityPatrolPath end


		CityAttackTest = FALSE;  //LT, used to check trigger
		Hill_Area_Trigger_Check = FALSE;  //LT, used to check area trigger


//**************ORIGINAL******************

		willRequestHelp = true; //?true or false
		helpRequestFrequency = 20.0; //?in secs
		attackerHelpRadius = 200; //?in meters
		defenderHelpRadius = 225; //?in meters
		lastHelpRequestTime = -100.0;
						
endfunction;


//----------------------------------------------------------------------------------------

function setWillRequestHelp (boolean setting);

	code

		if (setting and (not willRequestHelp)) then
			lastHelpRequestTime = 0.0;
		endif;
		willRequestHelp = setting;

endfunction;

//----------------------------------------------------------------------------------------

function update : integer;

	var

		boolean		processingPilotEvents;
		boolean		thinking;
		integer		pilotEventID;
		integer		pilotState;
		integer[20]		pilotEventParams;
		integer		curTarget;
		real			curTime;
		real[3]		myPos;
		real[3]		attackerPos;
		real			distanceToAttacker;
		integer		curStateHandle;


	code

		curTime = getTime;

//**************NEW***********************

		// Trigger for Hill Movers to Defend City
		if (City_Area_Trigger) then
			if (Not CityAttackTest) then
				CityAttackTest = true;
				if (curStateHandle <> AttackStateHandle) then
					trans CityDefend;
				endif;
			endif;
		endif;

		// Trigger for Hill Movers to Powerup and scan
		if (Hill_Area_Trigger) then
			if (NOT Hill_Area_Trigger_Check) then
				Hill_Area_Trigger_Check = TRUE;
				trans powerup;
			endif;
		endif;

//**************ORIGINAL******************


		//-----------------------------------
		//
		curStateHandle = getCurrentStateHandle;


		//--------------------------------------------------
		// Process the pilot events since the last update...
		numFunctionalWeapons = getWeapons(weaponList, 1);
		if (numFunctionalWeapons == 0) then
			trans noWeapons;
		endif;

		processingPilotEvents = TRUE;
		while (processingPilotEvents) do
			pilotEventID = getNextPilotEvent(pilotEventParams);
			if (pilotEventID == PILOT_EVENT_NONE) then
				processingPilotEvents = FALSE;
			else
				switch (pilotEventID)
					case PILOT_EVENT_TARGETED:
						curTarget = getTarget(-1);
						if (lastHelpRequestTime < (curTime - helpRequestFrequency)) then
							lastHelpRequestTime = curTime;
							if (willRequestHelp) then
								//distanceToAttacker = distanceToObject(-1, pilotEventParams[0]);
								getObjectPosition(pilotEventParams[0], attackerPos);
								getObjectPosition(-1, myPos);
								requestHelp(pilotEventParams[0], myPos, attackerHelpRadius, attackerPos, defenderHelpRadius, 1);
							endif;
						endif;
						numFunctionalWeapons = getWeapons(weaponList, 0);
						if (curStateHandle <> AttackStateHandle) then
							if ((numFunctionalWeapons > 0) and (curTarget == 0)) then
								coreAttack(pilotEventParams[0], TACORDER_PARAM_PURSUE);
								setState(AttackStateHandle);
							endif;
						endif;
						endcase;
					case PILOT_EVENT_ATTACK_ORDER:
						curTarget = getTarget(-1);
						if (curStateHandle <> AttackStateHandle) then
							if ((numFunctionalWeapons > 0) and (curTarget == 0))then
								coreAttack(pilotEventParams[0], TACORDER_PARAM_PURSUE);
								setState(AttackStateHandle);
							endif;
						endif;
						endcase;
					case PILOT_EVENT_FIRED_WEAPON:
						endcase;
				endswitch;
			endif;
		endwhile;

		return(0);

endfunction;

//----------------------------------------------------------------------------------------

state noWeapons;

	code

		setDebugString(-1, 3, "   NO WEAPONS ");
		if (objectClass(-1) == 2) then
			coreEject;
		else
			corePower(false);
		endif;
		
endstate;

//----------------------------------------------------------------------------------------

state CityDefend;

	code

		corePower(true);
		//coreRun = True;


		update;
		setDebugString(-1, 3, " CityDefend    ");
		corePatrol(startCityPatrolState, startCityPatrolPath, AttackStateHandle);
		resetOrders(1);

endstate;

//----------------------------------------------------------------------------------------

state powerup;

	code
		corePower(true);

		update;

		setDebugString(-1, 3, " GuardingHill     ");
		coreGuard(startPosition, -1, AttackStateHandle);
		resetOrders(1);
endstate;


//----------------------------------------------------------------------------------------

state attack;

	var

		integer		tacticState;

	code

		update;
		setDebugString(-1, 3, " ATTACK    ");
//		coreAttackTactic(0, TACORDER_PARAM_NONE, TACTIC_RIGHT_FLANK, tacticState);
		coreAttack(0, TACORDER_PARAM_PURSUE);
		resetOrders(1);
		transBack;

endstate;

//----------------------------------------------------------------------------------------

state start;

	code
		if (not poweredDown) then
			corePower(false);
			poweredDown = true;
		endif;

		update;
		setDebugString(-1, 3, " PoweredDown     ");

endstate;

//----------------------------------------------------------------------------------------

endfsm.

//****************************************************************************************
