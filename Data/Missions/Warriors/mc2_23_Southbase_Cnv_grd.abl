//****************************************************************************************//
//
//		Southbase Convoy - Mechs
//				Mechs
//
//      MISSION:  Mc2_23
//		Units That guard important vehicles in a convoy.
//		
//
//****************************************************************************************//

fsm mc2_23_Southbase_Cnv_grd;

	var

//**************ORIGINAL******************
		static WorldPosition		startPosition;
		static PatrolState		startPatrolState;
		static PatrolPath			startPatrolPath;
		static boolean			willRequestHelp;
		static real				lastHelpRequestTime;
		static real				helpRequestFrequency;
		static real				attackerHelpRadius;
		static real				defenderHelpRadius;
		static integer			AttackStateHandle;
		static boolean			poweredDown;

		static integer			numFunctionalWeapons;
		static integer[20]		weaponList;

//**************NEW***********************
		static PatrolState		startBase1PatrolState;
		static PatrolPath		startBase1PatrolPath;
		static PatrolState		startBase1RunPatrolState;
		static PatrolPath		startBase1RunPatrolPath;
		static boolean			CvA_Patrol_Triggered;
		static boolean			CvA_Patrol_End_Triggered;


//****************************************************************************************

function init;

    code

//**************ORIGINAL******************
		setDebugWindow(-1, -1);

		//---------------------------
		// Grab his start position...
		getObjectPosition(-1, startPosition);

		//-------------------
		// Look for movers...
		setTargetPriority(0, TARGET_PRIORITY_CURTARGET, -1, 150, CONTACT_CRITERIA_ENEMY + CONTACT_CRITERIA_VISUAL_OR_SENSOR + CONTACT_CRITERIA_NOT_DISABLED);
		setTargetPriority(1, TARGET_PRIORITY_MOVER, 0, 150, CONTACT_CRITERIA_ENEMY + CONTACT_CRITERIA_VISUAL_OR_SENSOR + CONTACT_CRITERIA_NOT_DISABLED);
		setTargetPriority(2, TARGET_PRIORITY_NONE, 0, 0, 0);

		AttackStateHandle = getStateHandle("attack");

//**************NEW***********************
		CvA_Patrol_Triggered = FALSE; //LT, used to check Patrol has been triggered
		CvA_Patrol_End_Triggered = FALSE;  //LT, used to check when NCV patrol ends

		//-------------------------------------------
		// Setup the Patrol here...
		//XL startBase1PatrolState Start

		startBase1PatrolState[0] = PATROL_TYPE_LINEAR;
		startBase1PatrolState[1] = 2;    //?num points
		startBase1PatrolState[2] = -1;    //?num cycles
		startBase1PatrolState[3] = PATROL_DIRECTION_FORWARD;
		startBase1PatrolState[4] = -1;    //reset cur point
		startBase1PatrolState[5] = -1;    //reset cur cycle
		startBase1PatrolState[6] = 0 + CONTACT_CRITERIA_ENEMY + CONTACT_CRITERIA_VISUAL_OR_SENSOR + CONTACT_CRITERIA_NOT_DISABLED;

		startBase1PatrolPath[0, 0] = -5696.000;
		startBase1PatrolPath[0, 1] = -5440.000;
		startBase1PatrolPath[1, 0] = -2368.000;
		startBase1PatrolPath[1, 1] = -5440.000;
		
		
		startBase1RunPatrolState[0] = PATROL_TYPE_LINEAR;
		startBase1RunPatrolState[1] = 2;    //?num points
		startBase1RunPatrolState[2] = 1;    //?num cycles
		startBase1RunPatrolState[3] = PATROL_DIRECTION_FORWARD;
		startBase1RunPatrolState[4] = -1;    //reset cur point
		startBase1RunPatrolState[5] = -1;    //reset cur cycle
		startBase1RunPatrolState[6] = 0 + CONTACT_CRITERIA_ENEMY + CONTACT_CRITERIA_VISUAL_OR_SENSOR + CONTACT_CRITERIA_NOT_DISABLED;

		startBase1RunPatrolPath[0, 0] = -5696.000;
		startBase1RunPatrolPath[0, 1] = -5440.000;
		startBase1RunPatrolPath[1, 0] = -2368.000;
		startBase1RunPatrolPath[1, 1] = -5440.000;

		//XLs startBase1PatrolPath end
		
//**************ORIGINAL******************

		willRequestHelp = false; //?true or false
		helpRequestFrequency = 20.0; //?in secs
		attackerHelpRadius = 200; //?in meters
		defenderHelpRadius = 225; //?in meters
		lastHelpRequestTime = -100.0;
						
endfunction;


//----------------------------------------------------------------------------------------

function setWillRequestHelp (boolean setting);

	code

		if (setting and (not willRequestHelp)) then
			lastHelpRequestTime = 0.0;
		endif;
		willRequestHelp = setting;

endfunction;

//----------------------------------------------------------------------------------------

function update : integer;

	var

		boolean		processingPilotEvents;
		boolean		thinking;
		integer		pilotEventID;
		integer		pilotState;
		integer[20]		pilotEventParams;
		integer		curTarget;
		real			curTime;
		real[3]		myPos;
		real[3]		attackerPos;
		real			distanceToAttacker;
		integer		curStateHandle;


	code

		curTime = getTime;

//**************NEW***********************

		// Trigger for CvA Movers to Start Base1 Escort
		if (Southbase_Attacked) then
			if (NOT CvA_Patrol_Triggered) then
				CvA_Patrol_Triggered = TRUE;
				trans Base1Escort;
	  	endif;
		endif;

		// Trigger for CvA Movers to Start Base1 Patrol
		if (Cv_End_Trigger) then
  		if (NOT CvA_Patrol_End_Triggered) then
  			CvA_Patrol_End_Triggered = TRUE;
  			trans Base1Patrol;
  		endif;
  	endif;
		
//**************End New******************


		//-----------------------------------
		//
		curStateHandle = getCurrentStateHandle;


		//--------------------------------------------------
		// Process the pilot events since the last update...
		numFunctionalWeapons = getWeapons(weaponList, 1);
		if (numFunctionalWeapons == 0) then
			trans noWeapons;
		endif;

		processingPilotEvents = TRUE;
		while (processingPilotEvents) do
			pilotEventID = getNextPilotEvent(pilotEventParams);
			if (pilotEventID == PILOT_EVENT_NONE) then
				processingPilotEvents = FALSE;
			else
				switch (pilotEventID)
					case PILOT_EVENT_TARGETED:
						curTarget = getTarget(-1);
						if (lastHelpRequestTime < (curTime - helpRequestFrequency)) then
							lastHelpRequestTime = curTime;
							if (willRequestHelp) then
								//distanceToAttacker = distanceToObject(-1, pilotEventParams[0]);
								getObjectPosition(pilotEventParams[0], attackerPos);
								getObjectPosition(-1, myPos);
								requestHelp(pilotEventParams[0], myPos, attackerHelpRadius, attackerPos, defenderHelpRadius, 1);
							endif;
						endif;
						numFunctionalWeapons = getWeapons(weaponList, 0);
						if (curStateHandle <> AttackStateHandle) then
							if ((numFunctionalWeapons > 0) and (curTarget == 0)) then
								coreAttack(pilotEventParams[0], TACORDER_PARAM_PURSUE);
								setState(AttackStateHandle);
							endif;
						endif;
						endcase;
					case PILOT_EVENT_ATTACK_ORDER:
						curTarget = getTarget(-1);
						if (curStateHandle <> AttackStateHandle) then
							if ((numFunctionalWeapons > 0) and (curTarget == 0))then
								coreAttack(pilotEventParams[0], TACORDER_PARAM_PURSUE);
								setState(AttackStateHandle);
							endif;
						endif;
						endcase;
					case PILOT_EVENT_FIRED_WEAPON:
						endcase;
				endswitch;
			endif;
		endwhile;

		return(0);

endfunction;

//----------------------------------------------------------------------------------------

state noWeapons;

	code

		setDebugString(-1, 3, "   NO WEAPONS ");
		if (objectClass(-1) == 2) then
			coreEject;
		else
			corePower(false);
		endif;
		
endstate;


//----------------------------------------------------------------------------------------

state Base1Escort;

	var

		integer				guardCharge;
		WorldPosition	guardChargePos;
		integer				guardChargeStatus;

	code
		setTargetPriority(0, TARGET_PRIORITY_CURTARGET, -1, 150, CONTACT_CRITERIA_ENEMY + CONTACT_CRITERIA_VISUAL_OR_SENSOR + CONTACT_CRITERIA_NOT_DISABLED);
		setTargetPriority(1, TARGET_PRIORITY_MOVER, 0, 250, CONTACT_CRITERIA_ENEMY + CONTACT_CRITERIA_VISUAL_OR_SENSOR + CONTACT_CRITERIA_NOT_DISABLED);
		setTargetPriority(2, TARGET_PRIORITY_NONE, 0, 0, 0);

		//coreRun = TRUE;
		update;
		setDebugString(-1, 3, "   ESCORT    ");
		
		//select my guard charge
		if (guardCharge <> 0) then
			guardChargeStatus = objectStatus(guardCharge);
			if ((guardChargeStatus == 1) or (guardChargeStatus == 2)) then
				guardCharge = 0;
			endif;
		endif;

		if (guardCharge == 0) then
			if ((objectStatus(741) <> OBJ_STATUS_DISABLED) and (objectStatus(741) <> OBJ_STATUS_DESTROYED)) then
				guardCharge = 741;	//HQ
			else
				if ((objectStatus(742) <> OBJ_STATUS_DISABLED) and (objectStatus(742) <> OBJ_STATUS_DESTROYED)) then
				guardCharge = 742; //RP
					else
						trans RunToBase1;
				endif;
			endif;
		endif;

		if (guardCharge == 0) then
			getObjectPosition(-1, guardChargePos);
			setMoveArea(guardChargePos, -1.0);
			coreGuard(guardChargePos, -1, AttackStateHandle);
		else
			getObjectPosition(guardCharge, guardChargePos);
			setMoveArea(guardChargePos, 100.0);
			coreEscort(guardCharge, 40.0, 135.0, -1);
		endif;


endstate;

//----------------------------------------------------------------------------------------

state RunToBase1;

	code

		//coreRun = TRUE;
//		MoverRunningToBase = TRUE;
		update;
		setDebugString(-1, 3, "  RunToBase1     ");
		corePatrol(startBase1RunPatrolState, startBase1RunPatrolPath, AttackStateHandle);
		trans Base1Guard;
		resetOrders(1);
endstate;

//----------------------------------------------------------------------------------------

state Base1Guard;

	code

		//coreRun = FALSE;
		update;
		setDebugString(-1, 3, "  Base1Guard     ");
		coreGuard(startPosition, -1, AttackStateHandle);
		resetOrders(1);
endstate;

//----------------------------------------------------------------------------------------

state attack;

	var

		integer		tacticState;

	code
		Southbase_Attacked = TRUE;

		update;
		setDebugString(-1, 3, " ATTACK    ");
//		coreAttackTactic(0, TACORDER_PARAM_NONE, TACTIC_RIGHT_FLANK, tacticState);
		coreAttack(0, TACORDER_PARAM_PURSUE);
		resetOrders(1);
		transBack;

endstate;

//----------------------------------------------------------------------------------------

state start;

	code
	
		
		update;
		setDebugString(-1, 3, "  Guarding     ");
		coreGuard(startPosition, -1, AttackStateHandle);
		resetOrders(1);
endstate;

//----------------------------------------------------------------------------------------

endfsm.

//****************************************************************************************
